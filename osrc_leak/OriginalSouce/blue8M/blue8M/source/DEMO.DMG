
;===============================================;
;						;
;      DEMO_animetion				;
;						;
;===============================================;

	include		common.def
	include		se.def
	include		effect.def
	include		banktool.def
	include		sgb_col.def
	include		fntequ.def
	include		..\data\waza_se.h
	include		macro.h
	include		group.def
	include		pm_debug.def

	public		koukan_demo_oya
	public		koukan_demo_ko
	public		test_demo
	public		demo_objset
	public		put_title
	public		demo_tit_dat

	extern		bank_push_call
	extern		oam_clr
	extern		set_oam_buf16
	extern		allow_effect
	extern		real_effect
	extern		block_cls
	extern		block_move
	extern		set_ef_cram
	extern		wait_vb
	extern		put_wait
	extern		wait_vb_s
	extern		wait_cont
	extern		play
	extern		effect_ready
	extern		bank2bank
	extern		ready2ready
	extern		put_msg
	extern		prt_mons_chr2
	extern		lcdc_stop
	extern		lcdc_on
	extern		gyaarth
	extern		gyaarth_play
	extern		badge_chrset
	extern		dvram_cls
	extern		n_to_w
	extern		w_to_n
	extern		n_to_b
	extern		b_to_n
	extern		chrset
	extern		get_monsadr
	extern		effect_data
	extern		cont_repeat
	extern		color_set
	extern		color_rewrite
	extern		put_all
	extern		put_window
	extern		put_dec
	extern		puthaji_yoko
	extern		puthaji_tate
	extern		put_win_msg
	extern		get_mons_name
	extern		prt_mons_chr
	extern		muswngo
	extern		muskusuri
	extern		color_reset
	extern		monschr_move
	extern		monschr_put2
	extern		memset

	extern		musnidopyon1
	extern		musnidopyon2
	extern		musgengerhand
	extern		musgengerpunch
	extern		musnidojump

	extern		musstartdemo

WSE_NO		equ	0


bank10	group	G_BANK10

;============================================================;
;   KOUKAN_DEMO                                              ;
;============================================================;
koukan_demo_oya:
	ld	a,(usr_buf + MY_MONS_No_@)	; ld	a,(usr_buf)
	ld	(usr_buf + USR_BUF_END -1),a	; ld	(usr_buf + 28),a
	ld	a,(usr_buf + YOUR_MONS_No_@)	; ld	a,(usr_buf + 1)
	ld	(usr_buf + USR_BUF_END),a	; ld	(usr_buf + 29),a

	ld	de,oya_tbl
	jr	demo_play

koukan_demo_ko:
	ld	a,(usr_buf + YOUR_MONS_No_@)	; ld	a,(usr_buf + 1)
	ld	(usr_buf + USR_BUF_END -1),a	; ld	(usr_buf + 28),a
	ld	a,(usr_buf + MY_MONS_No_@)	; ld	a,(usr_buf)
	ld	(usr_buf + USR_BUF_END),a	; ld	(usr_buf + 29),a

	ld	de,ko_tbl

demo_play:
	ld	a,(my_lvl)
	push	af
	ld	a,(headery)
	push	af
	ld	a,(headerx)
	push	af
	xor	a
	ld	(my_lvl),a			; (my_lvl) = 0
	ld	(headery),a
	ld	(headerx),a			; (headerx)=(headery) = 0

	push	de
main_loop$:
	pop	de				; DE = { &oya_tbl[], &ko_tbl[] }
	ld	a,(de)
	cp	0ffh				; I—¹ƒR[ƒh  $FF
	jr	z,exit$

	inc	de
	push	de

	ld	hl,koukan_table
	add	a,a
	ld	c,a
	ld	b,0
	add	hl,bc				; HL'= &koukan_table[(DE)*2]
	ld	a,(hli)
	ld	h,(hl)
	ld	l,a				; HL= (HL'+1)(HL')

	ld	de,main_loop$
	push	de
	jp	(hl)
exit$:
	pop	af
	ld	(headerx),a
	pop	af
	ld	(headery),a			; (headerx),(headery) ‚ğŒ³’l‚É–ß‚·
	pop	af
	ld	(my_lvl),a			; (my_lvl) ‚ğŒ³’l‚É–ß‚·
	ret

oya_tbl:	; ƒV[ƒPƒ“ƒX”Ô†
	db	ready_no
	db	chrset_no
	db	unitset_no
	db	suikomu_no
	db	tensour_no
	db	wait_no
	db	msgready_no
	db	msg1_no
	db	msg2_no
	db	msg3_no
	db	tensoul_no
	db	msgready_no
	db	unitset_no
	db	hakidasu_no
	db	wait_no
	db	end_no
	db	0ffh

ko_tbl:		; ƒV[ƒPƒ“ƒX”Ô†
	db	ready_no
	db	msgready_no
	db	msg5_no
	db	msg3_no
	db	namechange_no
	db	tensour_no
	db	namechange_no
	db	msgready_no
	db	unitset_no
	db	hakidasu_no
	db	msgscr_no
	db	chrset_no
	db	unitset_no
	db	suikomu_no
	db	namechange_no
	db	tensoul_no
	db	namechange_no
	db	wait_no
	db	msgready_no
	db	msg1_no
	db	end_no
	db	0ffh


koukan_table:
	initndw	koukan_ready	,ready_no
	ndw	koukan_chrset	,chrset_no
	ndw	koukan_unitset	,unitset_no
	ndw	koukan_suikomu	,suikomu_no
	ndw	koukan_hakidasu ,hakidasu_no
	ndw	koukan_tensou_r	,tensour_no
	ndw	koukan_tensou_l ,tensoul_no
	ndw	koukan_wait	,wait_no
	ndw	koukanmsg_ready	,msgready_no
	ndw	koukanmsg1	,msg1_no
	ndw	koukanmsg2	,msg2_no
	ndw	koukanmsg3	,msg3_no
	ndw	koukanmsg4	,msg4_no
	ndw	koukanmsg5	,msg5_no
	ndw	koukan_end	,end_no
	ndw	koukanmsg_scr	,msgscr_no
	ndw	name_change	,namechange_no


koukan_wait:
	ld	c,100
	jp	wait_vb_s		; ‚P‚O‚OƒtƒŒ[ƒ€‘Ò‚¿


koukan_wait2:
	ld	a,1
	ld	(all_put_req),a
	call	put_wait		; ‚RƒtƒŒ[ƒ€‘Ò‚¿ VBLANK’†‚Éall_put()‚ğƒR[ƒ‹‚·‚é
	xor	a
	ld	(all_put_req),a
	ret


koukan_wait3:
	ld	c,80
	jp	wait_vb_s		; ‚W‚OƒtƒŒ[ƒ€‘Ò‚¿


k_dvram_cls:				; dmy_vram[] = spc@
	ld	hl,dmy_vram
	ld	bc,20*18
	ld	a,7fh
	jp	memset


koukan_ready:
	call	k_dvram_cls		; dmy_vram[] = spc@
	call	lcdc_stop
	ld	hl,koukan_anime
	ld	de,09000h + 0310h
	ld	bc,49*16
	ld	a,00eh
	call	chrset			; chrset($9310, 0E:koukan_anime, 49*16)

	ld	hl,49*16 + koukan_anime
	ld	de,08000h + 7c0h
	ld	bc,4*16
	ld	a,00eh
	call	chrset			; chrset($807C, 0E:koukan_anime+49*16, 4*16)

	ld	hl,09800h		;bg_clear
	ld	bc,0800h
	ld	a,spc@
	call	memset			; $9800[0..7FF] = spc@ 

	call	oam_clr
	ld	a,0ffh
	ld	(oam_flg),a		; (oam_flg) = $FF
	ld	hl,obs_system
	set	6,(hl)			; (obs_system).<6> = 1

	ld	a,(sgb_flg)
	and	a
	ld	a,0e4h
	jr	z,z1$
	ld	a,0f0h
z1$:
	ld	(OBP0),a		; (OBP0) = (sgb_flg) ? $F0 : $E4 

	call	lcdc_on

	xor	a
	ld	(all_put_req),a

	ld	a,(usr_buf + MY_MONS_No_@)	; ©•ª‚ª‘—‚éƒ|ƒPƒ‚ƒ“”Ô†
	ld	(in_dat),a			
	call	get_mons_name		; return table_data[] = ©•ª‚ª‘—‚éƒ|ƒPƒ‚ƒ“–¼

	ld	hl,table_data
	ld	de,str_buf
	ld	bc,MONS_NAME_LEN
	call	block_move		; str_buf[] = ©•ª‚ª‘—‚éƒ|ƒPƒ‚ƒ“–¼

	ld	a,(usr_buf + YOUR_MONS_No_@)	; ‘Šè‚ª‘—‚éƒ|ƒPƒ‚ƒ“”Ô†
	ld	(in_dat),a			
	jp	get_mons_name		; return table_data[] = ‘Šè‚ª‘—‚éƒ|ƒPƒ‚ƒ“–¼


monschrset:
	ld	a,0d0h
	ld	(OBP1),a		; (OBP1) = $D0
	ld	b,1ch
	ld	hl,monschr_move
	jp	bank_push_call		; farcall	1C:monschr_move()


name_change:				; my_name[] <-> gein_name[] ‚ğŒğŠ·‚·‚é
	ld	hl,my_name		; iƒeƒ“ƒ|ƒ‰ƒŠ‚Æ‚µ‚Ä yes_no_map[] ‚ğg—p‚µ‚Ä‚¢‚éj
	ld	de,yes_no_map
	ld	bc,MONS_NAME_LEN
	call	block_move		; yes_no_map[] = my_name[]

	ld	hl,gein_name
	ld	de,my_name
	ld	bc,MONS_NAME_LEN
	call	block_move		; my_name[] = gein_name[]

	ld	hl,yes_no_map 
	ld	de,gein_name
	ld	bc,MONS_NAME_LEN
	jp	block_move		; gein_name[] = yes_no_map[]


koukan_end:
	xor	a
	call	color_reset		; (OBP0),(OBP1),(BGP)‚ğƒZƒbƒg
	ld	hl,obs_system
	res	6,(hl)			; (obs_system).<6> = 0
	ret


koukan_chrset:
	ld	a,0abh
	ld	(LCDC),a		; OBJ 8x8,  BG chr $8800 src $9C00,  Window $9800
	ld	a,80
	ld	(window_y),a		; (window_y) = 80

 ifn	ASSEMBLE__ENGLISH
	ld	a,086h			; i’ˆÓj‘—‚é‘¤–¼‘OƒEƒBƒ“ƒhƒE‚Ì˜g‚ğ‚PƒLƒƒƒ‰•ª¶‘¤‚É‘å‚«‚­
 else					; ‚µ‚½‚Ì‚ÅƒXƒNƒ[ƒ‹‚Ìˆ×‚É‰E‚É‰B‚·ƒEƒBƒ“ƒhƒE‚Ì‰ŠúˆÊ’u‚ğ‚P
	ld	a,07eh			; ƒLƒƒƒ‰•ª‰E‚Éİ’è‚µ‚Ä‚¨‚­
 endif
	ld	(WX),a			; (WX) = $7E
	ld	(headerx),a		; (headerx) = $7E

	xor	a
	ld	(all_put_req),a

 ifn	ASSEMBLE__ENGLISH
	SET_WIN_POS	4,0,15,7	;window set
 else
	SET_WIN_POS	5,0,15,7	;window set
 endif
	call	put_window
	call	okuru_gawa
	
	ld	b,098h			;window put to 9800h
	call	put_all
;	call	koukan_wait2

	call	dvram_cls
	ld	a,(usr_buf + MY_MONS_No_@)	; ©•ª‘¤‚Ìƒ|ƒPƒ‚ƒ“”Ô†
	call	trademons_pickup
	
	ld	a,07eh			;scroll
loop1$:
	push	af
	call	wait_vb
	pop	af
	ld	(WX),a			; (WX)      = $7E -> --(WX) -> $00
	ld	(headerx),a		; (headerx) = $7E -> --(headerx) -> $00
	dec	a
	dec	a
	and	a
	jr	nz,loop1$

	call	koukan_wait3		; ‚W‚OƒtƒŒ[ƒ€‘Ò‚¿

	ld	a,173			;smoke_effect
	call	effect_call
	ld	a,170			;smoke_effect
	call	effect_call

	ld	a,(usr_buf + MY_MONS_No_@)
	call	gyaarth_play

	xor	a
	ld	(all_put_req),a
	ret


koukan_unitset:
	call	k_dvram_cls		; dmy_vram[] = spc@
	ld	b,098h			;window put to 9800h
	call	put_all
	
	ld	b,COL_START
	call	color_set

	ld	hl,32*4 + 12 +09c00h
	call	line_set2
	ld	a,20*8
	ld	(headerx),a
	call	wait_vb

	ld	a,08bh
	ld	(LCDC),a
	
;	ld	a,1
;	ld	(all_put_req),a

	ld	hl,20*2 + 6 + dmy_vram
	ld	b,7
	call	bg_change2

	call	koukan_wait2

;	xor	a
;	ld	(all_put_req),a

	ld	a,muskusuri
	call	play

	ld	c,014h			;unit deru
loop$:
	ld	a,(headerx)
	add	a,4
	ld	(headerx),a
	dec	c
	jr	nz,loop$
	ret


koukan_suikomu:
	ld	a,171			;suikomi_effect
	call	effect_call

	ld	c,10
	call	wait_vb_s

	ld	a,0e4h
	ld	(OBP0),a
	xor	a
	ld	(el_c),a
	ld	bc,02060h 
loop$:
	push	bc
	xor	a
	ld	de,obj_move_tbl$
	call	set_oam_buf16

	ld	a,(el_c)
	xor	1
	ld	(el_c),a

	add	a,7eh
	ld	hl,oam_buf + 2
	ld	de,4
	ld	c,e
loop1$:
	ld	(hl),a
	add	hl,de
	dec	c
	jr	nz,loop1$
	
	call	put_wait

	pop	bc

	ld	a,c
	add	a,4
	ld	c,a
	cp	0a0h
	jr	nc,exit$

	ld	a,muswngo
	call	play

	jr	loop$
exit$:
	call	oam_clr
	ld	a,1
	ld	(all_put_req),a
	call	dvram_cls
	ld	b,098h			; 9800h clear
	call	put_all
	call	put_wait
	xor	a
	ld	(all_put_req),a
	ret


obj_move_tbl$:
	db	7eh,00h,7eh,20h,7eh,40h,7eh,60h


koukan_hakidasu:
	ld	a,172			;hakidasi_effect
	call	effect_call

	call	koukanmsg_ready
 ifn	ASSEMBLE__ENGLISH
	SET_WIN_POS	4,10,15,17	;window set
 else
	SET_WIN_POS	5,10,15,17	;window set
 endif
	call	put_window
	call	ukeru_gawa

	call	koukan_wait2

	ld	a,1
	ld	(all_put_req),a

	ld	a,(usr_buf + YOUR_MONS_No_@)	; ‘Šè‘¤ƒ|ƒPƒ‚ƒ“”Ô†
	call	trademons_pickup

	ld	a,173				;smoke_effect
	call	effect_call

	ld	a,1
	ld	(all_put_req),a

	ld	a,(usr_buf + YOUR_MONS_No_@)
	call	gyaarth_play

	call	koukan_wait

 ifn	ASSEMBLE__ENGLISH
	S_POS	4,10
	ld	bc,080ch
 else
	S_POS	5,10
	ld	bc,070bh
 endif
	call	block_cls
	jp	koukanmsg4

	
koukan_tensou_r:			; ‰E•ûŒüƒXƒNƒ[ƒ‹
	call	tensou_ready		; ƒLƒƒƒ‰ƒNƒ^‚ÌƒZƒbƒgA(headerx)=0,(window_y)=144 etc
	ld	a,1
	ld	(ef_count1),a		;scroll_houkou	‰E•ûŒü

	ld	a,0e4h
	ld	(OBP0),a
	ld	a,054h
	ld	(effect_x),a		; OBJ Xpos = $54
	ld	a,01ch
	ld	(effect_y),a		; OBJ Ypos = $1C	S_POS(9.5, 1.5)

	ld	a,(usr_buf + USR_BUF_END -1)	; ld	a,(usr_buf + 28)
	ld	(usr_buf + USR_BUF_END -2),a	; ld	(usr_buf + 27),a

	call	koukan_objset		; ‚n‚a‚iƒLƒƒƒ‰ƒNƒ^‚Æ‚S‘g‚Ì‚Q~‚Q‚Ì‚n‚a‚i‚ğƒZƒbƒg‚·‚é

;	ld	a,1
;	ld	(all_put_req),a

	call	game_boy1_put		; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÆƒP[ƒuƒ‹‚ÌŠG‚ğ•`‰æ‚·‚é  S_POS(5,3)
	call	koukan_wait2		; (all_put_req)=1, put_wait(), (all_put_req)=0 ;

;	xor	a
;	ld	(all_put_req),a

	call	line_set1		; S_POS(0,4)`(19,4) = $5EiƒP[ƒuƒ‹‚ÌŠGj‚ğ•`‰æ
	ld	hl,32*4 + 12 + 09c00h	; SCR_POS(12,4)(VRAM’¼‘‚«)
	call	line_set2		; haji_buf[2*20] = S_POS(0,4)`‚S‚OŒÂ•ª
					; haji_put_buf[0]=2, [1][2]= &SCR_POS(12,4)(VRAM’¼‘‚«)
	ld	b,06h			; ‚U‰ñ•ªi‚P‚QƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	ld	a,1
	ld	(all_put_req),a

	call	line_set1		;yoko_line only
					; S_POS(0,4)`(19,4) = $5EiƒP[ƒuƒ‹‚ÌŠGj‚ğ•`‰æ
	ld	b,04h			; ‚S‰ñ•ªi‚WƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	call	game_boy2_put		; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÆƒP[ƒuƒ‹‚ÌŠG‚ğ•`‰æ‚·‚é  S_POS(7,8)

	ld	b,06h			; ‚U‰ñ•ªi‚P‚QƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	xor	a
	ld	(all_put_req),a

	call	koukan_objmove		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚w‚xˆÚ“®‚³‚¹‚é
	jp	oam_clr

	
koukan_tensou_l:			; ¶•ûŒüƒXƒNƒ[ƒ‹
	call	tensou_ready		; ƒLƒƒƒ‰ƒNƒ^‚ÌƒZƒbƒgA(headerx)=0,(window_y)=144 etc
	xor	a
	ld	(ef_count1),a		;scroll_houkou	¶•ûŒü

	ld	a,064h
	ld	(effect_x),a		; OBJ Xpos = $64		
	ld	a,044h
	ld	(effect_y),a		; OBJ Ypos = $44	S_POS(11.5, 6.5)

	ld	a,(usr_buf + USR_BUF_END)	; ld	a,(usr_buf + 29)
	ld	(usr_buf + USR_BUF_END -2),a	; ld	(usr_buf + 27),a

	call	koukan_objset		; ‚n‚a‚iƒLƒƒƒ‰ƒNƒ^‚Æ‚S‘g‚Ì‚Q~‚Q‚Ì‚n‚a‚i‚ğƒZƒbƒg‚·‚é

;	inc	a
;	ld	(all_put_req),a

	call	game_boy2_put		; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÆƒP[ƒuƒ‹‚ÌŠG‚ğ•`‰æ‚·‚é  S_POS(7,8)
	call	koukan_wait2		; (all_put_req)=1, put_wait(), (all_put_req)=0 ;

;	xor	a
;	ld	(all_put_req),a

	call	line_set1		; S_POS(0,4)`(19,4) = $5EiƒP[ƒuƒ‹‚ÌŠGj‚ğ•`‰æ
	ld	hl,32*4 + 20 + 09c00h	; SCR_POS(20,4)(VRAM’¼‘‚«)
	call	line_set2		; haji_buf[2*20] = S_POS(0,4)`‚S‚OŒÂ•ª
					; haji_put_buf[0]=2, [1][2]= &SCR_POS(20,4)(VRAM’¼‘‚«)
	call	koukan_objmove		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚w‚xˆÚ“®‚³‚¹‚é

	ld	b,06h			; ‚U‰ñ•ªi‚P‚QƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	ld	a,1
	ld	(all_put_req),a

	call	line_set1		;yoko_line only
					; S_POS(0,4)`(19,4) = $5EiƒP[ƒuƒ‹‚ÌŠGj‚ğ•`‰æ
	ld	b,04h			; ‚S‰ñ•ªi‚WƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	call	game_boy1_put		; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÆƒP[ƒuƒ‹‚ÌŠG‚ğ•`‰æ‚·‚é  S_POS(5,3)

	ld	b,06h			; ‚U‰ñ•ªi‚P‚QƒLƒƒƒ‰•ª‰¡ƒXƒNƒ[ƒ‹‚·‚éj
	call	koukan_scroll		; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚a‚f‚ğ‰¡ƒXƒNƒ[ƒ‹‚³‚¹‚é

	xor	a
	ld	(all_put_req),a
	jp	oam_clr

	
tensou_ready:
	ld	a,1
	ld	(all_put_req),a
	call	dvram_cls		; dmy_vram[] = spc@
	xor	a
	ld	(all_put_req),a
	call	monschrset
	call	wait_vb

	ld	a,0abh			; OBJ 8x8,  BG chr $8800, src $9C00,  Window $9800
	ld	(LCDC),a

	xor	a
	ld	(headerx),a		; (headerx) = 0
	ld	a,144
	ld	(window_y),a		; (window_y) = 144
	ret


game_boy1_put:				; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÌŠG‚ğ•`‰æ
	call	k_dvram_cls		; dmy_vram[] = spc@
	ld	hl,20*4 + 11 + dmy_vram
	ld	a,02ch + 49		;conecter no.
	ld	(hli),a			;	S_POS(11,4)= $5D (ƒRƒlƒNƒ^[‚ÌŠGj

	ld	a,02dh + 49		;yoko_line no.
	ld	c,8
loop$:
	ld	(hli),a			;	S_POS(12,4)`(19,4)= $5EiƒP[ƒuƒ‹‚ÌŠGj
	dec	c
	jr	nz,loop$
	
	ld	hl,20*3 + 5 + dmy_vram	;game_boy1 put	S_POS(5,3)iƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÌŠGj
	ld	b,6
	call	bg_change2
;	ld	a,B_DEMO_ZOOM
;	call	bank2bank

 ifn	ASSEMBLE__ENGLISH
	SET_WIN_POS	4,12,12,15	;window set
	call	put_window
	S_POS	5,14
 else
	SET_WIN_POS	12,7,18,10	;window set
	call	put_window
	S_POS	13,9
 endif
	ld	de,my_name
	call	put_msg
	jp	wait_vb
	
	
game_boy2_put:				; ƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÌŠG‚ğ•`‰æ
	call	k_dvram_cls		; dmy_vram[] = spc@	

	ld	hl,20*4 + dmy_vram
	ld	a,02dh + 49		;yoko_line no.
	ld	c,14
loop$:
	ld	(hli),a			;	S_POS(0,4)`(13,4)= $5EiƒP[ƒuƒ‹‚ÌŠGj
	dec	c
	jr	nz,loop$
	
	ld	a,02eh + 49		;magari1_line no.
	ld	(hl),a			;	S_POS(14,4)= $5FiƒP[ƒuƒ‹‹È‚èŠGj

	ld	de,20
	add	hl,de
	ld	a,030h + 49		;tate_line no.
	ld	(hl),a			;	S_POS(14,5)`(14,8)= $61iƒP[ƒuƒ‹c‚ÌŠGj
	add	hl,de
	ld	(hl),a
	add	hl,de
	ld	(hl),a
	add	hl,de
	ld	(hl),a

	add	hl,de
	ld	a,02fh + 49		;magari2_line no.
	ld	(hld),a			;	S_POS(14,9)= $60iƒP[ƒuƒ‹‹È‚èŠGj

	ld	a,02ch + 49		;conecter no.
	ld	(hl),a			;	S_POS(13,9)= $5DiƒRƒlƒNƒ^[‚ÌŠGj

	ld	hl,20*8 + 7 + dmy_vram	;game_boy2 put	S_POS(7,8)iƒQ[ƒ€ƒ{[ƒC–{‘Ì‚ÌŠGj
	ld	b,6
	call	bg_change2
;	ld	a,B_DEMO_ZOOM
;	call	bank2bank

 ifn	ASSEMBLE__ENGLISH
	SET_WIN_POS	6,0,14,3	;window set
	call	put_window
	S_POS	7,2
 else
	SET_WIN_POS	0,10,6,13	;window set
	call	put_window
	S_POS	1,12
 endif
	ld	de,gein_name
	call	put_msg
	jp	wait_vb


line_set1:
	call	k_dvram_cls		; dmy_vram[] = spc@
	ld	hl,20*4 + dmy_vram
	ld	a,02dh + 49		;yoko_line no.
	ld	c,20
loop$:
	ld	(hli),a			; 	S_POS(0,4)`(19,4) = $5EiƒP[ƒuƒ‹‚ÌŠGj
	dec	c
	jr	nz,loop$
	ret


line_set2:				
	push	hl			;hl = line put address
	ld	hl,20*4 + dmy_vram	;	S_POS(0,4)
	call	puthaji_yoko		; haji_buf[2*20] = S_POS(0,4)`‚S‚OŒÂ•ª
	pop	hl

	ld	a,h
	ld	(haji_put_buf + 2),a
	ld	a,l
	ld	(haji_put_buf + 1),a
	ld	a,2
	ld	(haji_put_buf),a

	ld	c,10
	jp	wait_vb_s


koukan_scroll:
loop1$:
	ld	a,(ef_count1)
	ld	e,a
	ld	d,8		; ‚W‰ñŒJ‚è•Ô‚µiŒv (headerx)-=16 ‚Ü‚½‚Í (headerx)+=16 )
loop$:
	ld	a,e
	dec	a		; cp 1
	jr	z,right$
left$:				; (ef_count1) == 0 (!=1)‚Ì‚Æ‚«
	ld	a,(headerx)
	sub	2		;	(headerx) -= 2
	jr	next$
right$:				; (ef_count1) == 1 ‚Ì‚Æ‚«
	ld	a,(headerx)
	add	a,2		;	(headerx) += 2
next$:
	ld	(headerx),a
	call	wait_vb		; ‚u‚a‚k‚`‚m‚j’†‚É (SCX)=(headerx) ‚ğ‚µ‚Ä‚¢‚é "VBLANK.DMG"

	dec	d
	jr	nz,loop$

	call	koukan_objchange ; ‚Q~‚Q‚n‚a‚i ‚T‘g•ª chrNo. Bit6 ‚ğ”½“]‚³‚¹‚é
	dec	b		; B reg ‰ñŒJ‚è•Ô‚·
	jr	nz,loop1$
	ret


koukan_objchange:
	push	de
	push	bc
	push	hl
	ld	a,(BGP)
	xor	03ch		;bg color_change  10 -> 01,01 -> 10
	ld	(BGP),a

	ld	hl,oam_buf + 2
	ld	de,4
	ld	c,20		; ‚Q~‚Q‚n‚a‚i ‚T‘g•ª
loop$:
	ld	a,(hl)
	xor	40h
	ld	(hl),a		; chrNo. ^= $40  Bit6 ‚ğ”½“]‚³‚¹‚é
	add	hl,de
	dec	c
	jr	nz,loop$
	
	pop	hl
	pop	bc
	pop	de
	ret


koukan_objset:			;el_c = chr_no. 
	ld	b,1ch
	ld	hl,monschr_put2
	call	bank_push_call
	call	maru_put		; ‚S‘g‚Ì‚Q~‚Q‚Ì‚n‚a‚i‚ğ oam_buf ‚ÉƒZƒbƒg‚·‚é

koukan_objset2:	
	ld	hl,oam_buf
	ld	c,20			; ‚T‘g•ªi‚Q~‚Q‚Ì‚n‚a‚ij
loop$:
	ld	a,(effect_y)
	add	a,(hl)
	ld	(hli),a			; Ypos += (effect_y)
	ld	a,(effect_x)
	add	a,(hl)
	ld	(hli),a			; Xpos += (effect_x)
	inc	hl
	inc	hl
	dec	c
	jr	nz,loop$
	ret
	

koukan_objmove:				; ‚Q~‚Q‚n‚a‚i‚ğ”½“]‚³‚¹‚È‚ª‚ç‚w‚xˆÚ“®‚³‚¹‚é
	ld	a,(ef_count1)		; (ef_count1) = {0:¶, 1:‰E} •ûŒüƒXƒNƒ[ƒ‹
	and	a
	jr	z,z1$
					; ‰E•ûŒüƒXƒNƒ[ƒ‹‚Ì‚Æ‚«
	ld	bc,0400h		; (effect_x)= 4 , (effect_y)= 0
	call	koukan_o_movesub
	ld	bc,000ah		; (effect_x)= 0 , (effect_y)= 0
	jr	koukan_o_movesub

z1$:					; ¶•ûŒüƒXƒNƒ[ƒ‹‚Ì‚Æ‚«
	ld	bc,00f6h		; (effect_x)= 0 , (effect_y)= -10
	call	koukan_o_movesub
	ld	bc,0fc00h		; (effect_x)= -4 , (effect_y)= 0

koukan_o_movesub:
	ld	a,b
	ld	(effect_x),a
	ld	a,c
	ld	(effect_y),a
	ld	d,4			; ‚S‰ñŒJ‚è•Ô‚·
loop$:
	call	koukan_objset2		; ‚T‘g•ªi‚Q~‚Q‚Ì‚n‚a‚ijYpos+=(effect_y) Xpos+=(effect_x)
	call	koukan_objchange	; ‚Q~‚Q‚n‚a‚i ‚T‘g•ª chrNo. Bit6 ‚ğ”½“]‚³‚¹‚é

	ld	c,8
	call	wait_vb_s

	dec	d
	jr	nz,loop$
	ret


maru_put:				; ‚S‘g‚Ì‚Q~‚Q‚Ì‚n‚a‚i‚ğ oam_buf ‚ÉƒZƒbƒg‚·‚é
	ld	hl,maru_tbl$
	ld	c,4
	xor	a
loop$:
	push	bc	
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	inc	hl
	push	hl
	inc	a
	push	af
	call	set_oam_buf16		; set_oam_buf16(DE data adrs, B y, C x, A oam_buf No.)
	pop	af			; ‚Q~‚Q‚Ì‚n‚a‚i‚ğƒZƒbƒg
	pop	hl
	pop	bc
	dec	c
	jr	nz,loop$
	ret

maru_tbl$:	; DE    y x 
	dw	maru1$,0808h
	dw	maru2$,0818h
	dw	maru3$,1808h
	dw	maru4$,1818h
maru1$:
	db	38h,10h,39h,10h,3ah,10h,3bh,10h		; chrNo,atrb,chrNo,atrb,...
maru2$:
	db	39h,30h,38h,30h,3bh,30h,3ah,30h
maru3$:
	db	3ah,50h,3bh,50h,38h,50h,39h,50h
maru4$:
	db	3bh,70h,3ah,70h,39h,70h,38h,70h
	

trademons_pickup:
	ld	(sel_item_no),a
	ld	(tbl_pos),a
	ld	(sgbcol_buf),a

;	call	dvram_cls
	ld	b,COL_SHINKA
	ld	c,0
	call	color_set

	ld	a,(all_put_req)
	xor	1
	ld	(all_put_req),a

;	ld	a,1
;	ld	(all_put_req),a
	
	call	get_monsadr
	S_POS	7,2
	call	prt_mons_chr

	ld	c,10
	jp	wait_vb_s


koukanmsg_ready:
	ld	a,1
	ld	(all_put_req),a
	call	dvram_cls
	ld	a,0e3h
	ld	(LCDC),a

	ld	a,7
	ld	(WX),a
	xor	a
	ld	(window_y),a
	ld	a,144
	ld	(headerx),a
	ret


koukanmsg_scr:
	ld	c,50
	call	wait_vb_s
loop$:
	call	wait_vb
	ld	a,(WX)
	inc	a
	inc	a
	ld	(WX),a
	cp	161
	jr	nz,loop$
	call	k_dvram_cls

	ld	c,10
	call	wait_vb_s

	ld	a,7
	ld	(WX),a
	ret


koukanmsg1:
	ld	hl,msg1$
	call	put_win_msg
	ld	c,200
	call	wait_vb_s
	jp	koukanmsg_scr

msg1$:
	extern	msg1_0_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg1_0_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á


koukanmsg2:
	ld	hl,msg1$
	call	put_win_msg
	call	koukan_wait3
	ld	hl,msg2$
	call	put_win_msg
	jp	koukan_wait3

msg1$:
	extern	msg1_1_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg1_1_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á

msg2$:
	extern	msg2_2_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg2_2_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á


koukanmsg3:
	ld	hl,msg1$
	call	put_win_msg
	call	koukan_wait3
	ld	hl,msg2$
	call	put_win_msg
	call	koukan_wait3
	jp	koukanmsg_scr

msg1$:
	extern	msg1_3_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg1_3_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á

msg2$:
	extern	msg2_4_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg2_4_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á


koukanmsg4:
	ld	hl,msg1$
	call	put_win_msg
	jp	koukan_wait3

msg1$:
	extern	msg1_5_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg1_5_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á


koukanmsg5:
	ld	hl,msg1$
	call	put_win_msg
	call	koukan_wait3
	ld	hl,msg2$
	call	put_win_msg
	jp	koukan_wait3

msg1$:
	extern	msg1_6_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg1_6_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á

msg2$:
	extern	msg2_7_DEMO
	db I_MSG2	; mvmsg’Ç‰Á
	dw msg2_7_DEMO	; mvmsg’Ç‰Á
	db 022h	; mvmsg’Ç‰Á
	db EOM	; put_msg_sI—¹ƒR[ƒh mvmsg’Ç‰Á

effect_call:
	ld	(effect_no),a
	xor	a
	ld	(anime_buf),a
	ld	a,B_EFFECT_SELECT
	jp	bank2bank


;============================================================;
;   TEST_DEMO                                                ;
;============================================================;
test_demo:
;	ld	a,1
;	ld	(all_put_req),a		;memory costs lot!
	xor	a
	ld	(joy_status),a
	inc	a
	ld	(all_put_req),a

	call	gamefreakset
	call	demo_main
z1$:
	call	n_to_w		
	xor	a
	ld	(headerx),a
	ld	(all_put_req),a
	call	oam_clr

	call	wait_vb
	ret
	
demo_main:
;-------screen_set--------------

	ld	b,COL_DEMO
	call	color_set

	ld	a,0e4h
	ld	(BGP),a
	ld	(OBP0),a
	ld	(OBP1),a

	xor	a
	ld	(headerx),a
;-------chr_set--------------------
	ld	b,3
	call	bg_change			;set BG chr (genger)

;	ld	a,008h
	ld	a,000h
	ld	(effect_x),a
	ld	a,050h
	ld	(effect_y),a
	ld	bc,0606h
	call	demo_objset

	ld	de,028ffh
	call	demo_scroll
	ret	c
;-------teki_footwork--------------
	ld	a,musnidopyon1
	call	play
	
	xor	a
	ld	(el_c),a
	ld	de,footwork1_tbl
	call	demo_move

	ld	a,musnidopyon2
	call	play
	
	ld	de,footwork3_tbl
	call	demo_move

	ld	c,10
;	call	demo_wait_s
	call	wait_cont
	ret	c

	ld	a,musnidopyon1
	call	play
	
	ld	de,footwork1_tbl
	call	demo_move

	ld	a,musnidopyon2
	call	play
	
	ld	de,footwork3_tbl
	call	demo_move

	ld	c,50-20
;	call	demo_wait_s
	call	wait_cont
	ret	c

;-------jiki_kougeki--------------
	ld	b,4
	call	bg_change
	
	ld	a,musgengerhand
	call	play

	ld	de,0401h
	call	demo_scroll

	ld	c,30
;	call	demo_wait_s
	call	wait_cont
	ret	c

	ld	b,5
	call	bg_change

	ld	a,musgengerpunch
	call	play

	ld	de,00800h
	call	demo_scroll

;-------teki_sakeru--------------
	ld	a,musnidopyon1
	call	play
	
	ld	a,36
	ld	(el_c),a
	ld	de,sakeru_tbl
	call	demo_move
	
	ld	c,50-20
;	call	demo_wait_s
	call	wait_cont
	ret	c

	ld	de,0401h
	call	demo_scroll

	ld	b,3
	call	bg_change

	ld	c,80-20
;	call	demo_wait_s
	call	wait_cont
	ret	c

;-------teki_kougeki--------------
	ld	a,musnidopyon1
	call	play

	xor	a
	ld	(el_c),a
	ld	de,footwork2_tbl
	call	demo_move

	ld	a,musnidopyon2
	call	play

	ld	de,footwork4_tbl
	call	demo_move

	ld	c,20
;	call	demo_wait_s
	call	wait_cont
	ret	c

	ld	a,36
	ld	(el_c),a
	ld	de,shagami_tbl
	call	demo_move
	
	ld	c,50-20
;	call	demo_wait_s
	call	wait_cont
	ret	c

	ld	a,musnidojump
	call	play

	ld	a,72
	ld	(el_c),a
	ld	de,attack_tbl
;	call	demo_move
	jp	demo_move
	
;	ld	c,80
;	jp	demo_wait_s
;	jp	wait_cont
;	ret


demo_move:
	ld	a,(de)
	cp	050h
	ret	z

	ld	(effect_y),a
	inc	de
	ld	a,(de)
	ld	(effect_x),a

	push	de
	ld	c,36
	call	demo_move_sub

	ld	c,5
	call	wait_vb_s

	pop	de
	inc	de
	jr	demo_move


demo_move_sub:
	ld	hl,oam_buf
	ld	a,(el_c)
	ld	d,a
loop$:
	ld	a,(effect_y)
	add	a,(hl)
	ld	(hli),a

	ld	a,(effect_x)
	add	a,(hl)
	ld	(hli),a

	ld	a,d
	ld	(hli),a

	inc	hl

	inc	d
	dec	c
	jr	nz,loop$
	ret


demo_objset:
	ld	hl,oam_buf
	ld	d,0
loop$:
	push	bc
	ld	a,(effect_y)
	ld	e,a
loop1$:
	ld	a,e
	add	a,008h
	ld	e,a
	ld	(hli),a

	ld	a,(effect_x)
	ld	(hli),a

	ld	a,d
	ld	(hli),a

	ld	a,080h
	ld	(hli),a
	inc	d

	dec	c
	jr	nz,loop1$

	ld	a,(effect_x)
	add	a,008h
	ld	(effect_x),a

	pop	bc
	dec	b
	jr	nz,loop$
	ret


screen_cls1:
	ld	hl,09c00h
	ld	bc,32*18
	jr	cls

screen_cls2:
	ld	hl,20*4 + dmy_vram
	ld	bc,20*10

cls:
	ld	(hl),0
	inc	hl
	dec	bc
	ld	a,b
	or	c
	jr	nz,cls
	ret


screenset_sub:
	ld	a,1
loop$:
	ld	(hli),a
	dec	c
	jr	nz,loop$
	ret


demo_scroll:
loop$:
	ld	a,e
	cp	0ffh
	jr	z,opning_scl$

	cp	1
	jr	z,right$
left$:
	ld	a,(headerx)
	dec	a
	dec	a
	jr	next$
opning_scl$:
	push	de
	ld	a,2
	ld	(effect_x),a
	xor	a
	ld	(effect_y),a
	ld	c,36
	call	demo_move_sub
	pop	de
right$:
	ld	a,(headerx)
	inc	a
	inc	a
next$:
	ld	(headerx),a

	push	de
	ld	c,2
;	call	demo_wait_s
	call	wait_cont
	pop	de
	ret	c

	dec	d
	jr	nz,loop$
	ret

	
bg_change:
	ld	hl,20*7 + 13 + dmy_vram
bg_change2:
	ld	c,0
	ld	a,B_DEMO_ZOOM
	jp	bank2bank


demo_se:
	ld	a,B_PLAY_DEMO_SE
	call	bank2bank
	ld	a,b
	jp	play

	
demochr_set:
	ld	hl,op_bg_dat
	ld	de,09000h
	ld	bc,060h*16
	ld	a,010h
	call	chrset

	ld	hl,demo_tit_dat
	ld	de,09600h
	ld	bc,20*16
	ld	a,010h
	call	chrset

	ld	hl,demo_tit_dat
	ld	de,08800h
	ld	bc,20*16
	ld	a,010h
	call	chrset

	ld	hl,op_obj_dat
	ld	de,08000h
	ld	bc,1728			;1728=36*16 + 36*16 + 36*16
	ld	a,010h
	jp	chrset


gamefreakset:
	ld	b,COL_TITLE
	call	color_set

	ld	b,1
	extern	opning_marushi
	ld	hl,opning_marushi
	call	bank_push_call

	ld	a,0e4h
	ld	(BGP),a
	ld	c,180
	call	wait_vb_s
	call	dvram_cls

	call	lcdc_stop

	xor	a
;	ld	(BGP),a
	ld	(event_fight_no),a

	call	screen_set
	call	demochr_set

	call	lcdc_on

	ld	hl,LCDC
	res	5,(hl)
	set	3,(hl)

;	ld	a,B_BG_CC2
;	call	bank2bank

	ld	c,40h
	call	wait_vb_s
	
 	ld	b,1ch
	extern	game_freak_put
	ld	hl,game_freak_put
	call	bank_push_call
	push	af
;;;	call	put_title			; "PRESENTS" ‚ğ•\¦‚·‚éi‰pŒê”Å‚Å‚Ííœ‚·‚éj
	pop	af
	jr	c,pass$

	ld	c,40
	call	wait_vb_s

pass$:
	ld	a,MUSIC_BANK3_NO		; Title Music Bank
	ld	(music_bank_flg),a
	ld	(music_bank_flg_back),a
	ld	a,musstartdemo
	ld	(music_flag),a			; 0 : SE / !0 : Music
	call	play

	call	screen_cls2
	call	oam_clr
	jp	put_wait


	public	screen_set
screen_set:
	call	screen_cls1

	ld	hl,dmy_vram 
	ld	c,20*4
	call	screenset_sub

	ld	hl,20*14 + dmy_vram
	ld	c,20*4
	call	screenset_sub

	ld	hl,09c00h 
	ld	c,32*4
	call	screenset_sub

	ld	hl,32*14 + 09c00h
	ld	c,32*4
	jp	screenset_sub


put_title:					; "PRESENTS" ‚ğ•\¦‚·‚é
 ifn	ASSEMBLE__ENGLISH
	ret					; i’ˆÓIj‰pŒê”Å‚Å‚Í PRESENTS ‚Ííœ‚·‚é
 else
	ld	hl,dmy_vram + 227		; S_POS(7,11)
	ld	de,title_tbl2
	jp	put_msg

title_tbl2:
	db	67h,68h,69h,6ah,6bh,6ch		; PRESENTS
	db	EOM
 endif
start_tbl:
	db	000h,000h,050h
footwork1_tbl:
	db	000h,000h,0feh,002h,0ffh,002h,001h,002h,002h,002h,050h
footwork3_tbl:
	db	000h,000h,0feh,0feh,0ffh,0feh,001h,0feh,002h,0feh,050h
sakeru_tbl:
	db	000h,000h,0f4h,006h,0f8h,006h,008h,006h,00ch,006h,050h
footwork2_tbl:
	db	000h,000h,0f8h,0fch,0fch,0fch,004h,0fch,008h,0fch,050h
footwork4_tbl:
	db	000h,000h,0f8h,004h,0fch,004h,004h,004h,008h,004h,050h
shagami_tbl:
	db	000h,000h,002h,000h,002h,000h,000h,000h,050h
attack_tbl:
;	db	0f8h,0f0h,0f9h,0f2h,0fah,0f4h,0fch,0f6h,050h
	db	0f8h,0f0h,0f9h,0f2h,0fah,0f4h,0fch,0f6h,050h
demo_tit_dat:
	include		..\data\demo_tit.dat
op_bg_dat:
	include		..\data\op_bg.dat
op_obj_dat:
  ifn	pokemon_type
   ifn	pokemon_type_blue
	;-----<blue version>-----
	include		..\data\op_purin.dat
   else
	;-----<red version>-----
	include		..\data\op_obj.dat
   endif
  else
	;-----<green version>-----
	include		..\data\op_obj.dat
  endif


okuru_gawa:					; 	‘—‚é‘¤
 ifn	ASSEMBLE__ENGLISH						;*----No. ‚O‚P‚U----*
	S_POS	5,0							;|                  |
	ld	de,koukandata_str		; "No." "oya/" "IDNo." 	;|ƒ|ƒbƒ|            |
	call	put_msg							;|                  |
									;|‚¨‚â^ƒvƒŒƒCƒ„[  |
	ld	a,(usr_buf)						;|                  |
	ld	(in_dat),a						;|IDNo. ‚T‚O‚S‚U‚V  |
	ld	a,B_GET_ORDER_NO					;*------------------*
	call	bank2bank			; return (in_dat)= ƒI[ƒ_[”Ô†

	S_POS	9,0
	ld	de,in_dat			; ©•ª‚ª‘—‚éƒ|ƒPƒ‚ƒ“”Ô†
	ld	bc,08103h			; 1byte 3keta	; b(7bit)=1 ( zero print )
	call	put_dec				; print monster number
	
	S_POS	5,2
	ld	de,str_buf			; ©•ª‚ª‘—‚éƒ|ƒPƒ‚ƒ“–¼
	call	put_msg

	S_POS	8,4
	ld	de,usr_buf + MY_OYA_NAME_@	; ©•ª‘¤‚Ì‚¨‚â–¼
	call	put_msg

	S_POS	8,6
	ld	de,usr_buf + MY_MONS_IDNo_@	; ©•ª‘¤‚Ìƒ|ƒPƒ‚ƒ“ IDNo.
	ld	bc,08205h			; 2byte 5keta	; b(7bit)=1 ( zero print )
	jp	put_dec				; print id-no

 else
	S_POS	6,0
	ld	de,koukandata_str		; "No." "oya/" "IDNo."
	call	put_msg

	ld	a,(usr_buf)
	ld	(in_dat),a
	ld	a,B_GET_ORDER_NO
	call	bank2bank

	S_POS	10,0
	ld	de,in_dat			; jibun ga okuru mons No
	ld	bc,08103h			; 1byte 3keta
						; b(7bit)=1 ( zero print )
	call	put_dec				; print monster number
	
	S_POS	6,2
	ld	de,str_buf			; jibun ga okuru mons name
	call	put_msg

	S_POS	9,4
    ;	ld	de,usr_buf + 4			; usr_buf 4~9
	ld	de,usr_buf + MY_OYA_NAME_@
	call	put_msg				; oya name

	S_POS	9,6
    ;	ld	de,usr_buf + 10
	ld	de,usr_buf + MY_MONS_IDNo_@
	ld	bc,08205h			; 2byte 5keta
						; b(7bit)=1 ( zero print )
	jp	put_dec				; print id-no
 endif

ukeru_gawa:					; 	ó‚¯‚é‘¤	;*----No. ‚O‚P‚U----*
 ifn	ASSEMBLE__ENGLISH						;|                  |
	S_POS	5,10							;|ƒ|ƒbƒ|            |
	ld	de,koukandata_str		; "No." "oya/" "IDNo."	;|                  |
	call	put_msg							;|‚¨‚â^ƒvƒŒƒCƒ„[  |
									;|                  |
	ld	a,(usr_buf + YOUR_MONS_No_@)				;|IDNo. ‚T‚O‚S‚U‚V  |
	ld	(in_dat),a						;*------------------*
	ld	a,B_GET_ORDER_NO
	call	bank2bank			; return (in_dat)= ƒI[ƒ_[”Ô†

	S_POS	9,10
	ld	de,in_dat			; ©•ª‚ªó‚¯æ‚éƒ|ƒPƒ‚ƒ“”Ô†
	ld	bc,08103h			; 1byte 3keta	; b(7bit)=1 ( zero print )
	call	put_dec				; print monster number
	
	S_POS	5,12
	ld	de,table_data			; ©•ª‚ªó‚¯æ‚éƒ|ƒPƒ‚ƒ“–¼
	call	put_msg

	S_POS	8,14
	ld	de,usr_buf + YOUR_OYA_NAME_@	; ‘Šè‘¤‚Ì‚¨‚â–¼
	call	put_msg

	S_POS	8,16
	ld	de,usr_buf + YOUR_MONS_IDNo_@	; ‘Šè‘¤‚Ìƒ|ƒPƒ‚ƒ“ IDNo.
	ld	bc,08205h			; 2byte 5keta	; b(7bit)=1 ( zero print )
	jp	put_dec				; print id-no

 else
	S_POS	6,10
	ld	de,koukandata_str		; "No." "oya/" "IDNo." 
	call	put_msg

    ;	ld	a,(usr_buf + 1)
	ld	a,(usr_buf + YOUR_MONS_No_@)
	ld	(in_dat),a
	ld	a,B_GET_ORDER_NO
	call	bank2bank

	S_POS	10,10
	ld	de,in_dat			; jibun ga ukeru mons No
	ld	bc,08103h			; 1byte 3keta
						; b(7bit)=1 ( zero print )
	call	put_dec				; print monster number
	
	S_POS	6,12
	ld	de,table_data			; jibun ga ukeru mons name
	call	put_msg

	S_POS	9,14
    ;	ld	de,usr_buf + 12			; usr_buf 12~17
	ld	de,usr_buf + YOUR_OYA_NAME_@
	call	put_msg				; oya name

	S_POS	9,16
    ;	ld	de,usr_buf + 18
	ld	de,usr_buf + YOUR_MONS_IDNo_@	; ‘Šè‘¤‚Ìƒ|ƒPƒ‚ƒ“ IDNo.
	ld	bc,08205h			; 2byte 5keta
						; b(7bit)=1 ( zero print )
	jp	put_dec				; print id-no

 endif


koukandata_str:
  ifn	pm_jmsg
	db	waku1@,waku1@,No@,dot@,cr@	; ==No.
	db	cr@				;
	db	o_,ya_,sura@,cr@		; ‚¨‚â^
	db	id@,No@,dot@			;
	db	EOM				; IDNo.
  else
	db	waku1@,waku1@,No@,dot@,cr@	; ==No.
	db	cr@				;
	db	usf_o,usf_t,sura@,cr@		; ‚¨‚â^
	db	id@,No@,dot@			;
	db	EOM				; IDNo.
  endif



banke	group	G_BANKe

koukan_anime:
	include		..\data\trade_an.dat

