	include	common.def
	include	pm_debug.def
	include	banktool.def


	public	set_dealer_mons

	extern	add_capsule_new
	extern	bank2bank
	extern	mul_any


banke	group	14
;********************************
;	set_dealer_mons		*
;********************************
set_dealer_mons:
	ld	a,(tuushin_flg)
	and	a	
	ret	nz

	ld	hl,gein_cap_tbl
	xor	a
	ld	(hli),a				; gein no monster suu wo clear
;	ld	a,0ffh
	dec	a
	ld	(hl),a

	ld	a,(dealer_no)
	dec	a
	add	a,a				; * 2
	ld	hl,dealer_mons_tbl
	ld	c,a
	ld	b,0
	add	hl,bc

	ld	a,(hli)
	ld	h,(hl)
	ld	l,a

	ld	a,(take_mons_ptn)
	ld	b,a
loop1$:
	dec	b
	jr	z,set$
loop2$:
	ld	a,(hli)
	and	a
	jr	nz,loop2$

	jr	loop1$

set$:
	ld	a,(hli)
	cp	0ffh
	jr	z,special_set$
	ld	(mons_level),a

set_loop$:
	ld	a,(hli)
	and	a
	jp	z,boss_waza_set$
	
	ld	(sel_item_no),a
	ld	a,1
	ld	(my_or_gein),a			; 1 = gein
	push	hl
	call	add_capsule_new
	pop	hl
	jr	set_loop$
	
special_set$:
	ld	a,(hli)
	and	a
	jr	z,boss_waza_set$
	
	ld	(mons_level),a
	ld	a,(hli)
	ld	(sel_item_no),a
	ld	a,1
	ld	(my_or_gein),a			; 1 = gein
	push	hl
	call	add_capsule_new
	pop	hl
	jr	special_set$



boss_waza_set$:
;	jp	set_gold$			; for debug

	ld	a,(dealer_no)
	ld	b,a
	ld	a,(take_mons_ptn)
	ld	c,a
	ld	hl,boss_waza_tbl$
rival_loop1$:
	ld	a,(hli)
	cp	0ffh
	jr	z,set_gold$
	cp	b			; dealer_no
	jr	nz,next_chk$
	ld	a,(hli)
	cp	c			; take_mons_ptn
	jr	nz,next_chk$

	ld	d,h
	ld	e,l
rivalwaza_loop$:
	ld	a,(de)
	inc	de
	and	a
	jp	z,set_gold$

	dec	a
	ld	hl,gein_cap_data + 8		; waza address
	ld	bc,CAPDATA_LEN
	call	mul_any

	ld	a,(de)
	inc	de
	dec	a
	ld	c,a
	ld	b,0
	add	hl,bc
	ld	a,(de)
	inc	de
	ld	(hl),a
	
	jr	rivalwaza_loop$

next_chk$:
	ld	a,(hli)
	and	a
	jr	nz,next_chk$

	jr	rival_loop1$


	
set_gold$:
	xor	a
	ld	de,yuuichi_gold
	ld	(de),a
	inc	de
	ld	(de),a
	inc	de
	ld	(de),a

	ld	a,(mons_level)
	ld	b,a				; loop counter

calc_loop$:
	ld	hl,dealer_gold + 1
	ld	c,2

	push	bc

	ld	a,B_BCD_ADD
	call	bank2bank

	pop	bc

	inc	de
	inc	de
	dec	b
	jr	nz,calc_loop$

	ret


boss_waza_tbl$:
; mushitori
	db	2,15,2,2,33,2,3,81,0
; tanpan
	db	1,14,1,4,90,0

; leader
	db	34,1,2,3,20,2,4,117,0
	db	35,1,2,4,61,0
	db	36,1,1,1,85,1,2,5,1,3,25,1,4,45,0
	db	37,1,1,3,72,2,1,75,3,1,80,0
	db	38,1,1,1,92,1,2,33,2,1,92,2,3,48
	db	3,1,92,3,2,38,4,1,141,4,2,104,4,3,94,4,4,92,0
	db	39,1,1,1,53,1,4,109,3,1,53,3,2,126,3,3,115,0
	db	40,1,1,1,148,2,1,134,2,4,149,3,1,149,0
	db	29,3,1,3,90,2,2,104,3,1,89,3,3,87,4,1,89,4,2,43
	db	4,3,87,5,1,157,5,4,89,0

; shitennou
	db	44,1,1,1,61,2,3,58,3,1,94,3,2,57,4,3,142,5,3,59,0
	db	33,1,1,1,157,1,2,103,1,4,91,2,3,7,2,4,104,3,1,24,3,2,25
	db	3,4,104,4,1,157,4,2,103,4,4,89,5,2,2,5,3,70,0
	db	46,1,1,2,164,1,3,122,1,4,72,2,2,92,2,4,141,3,2,122
	db	4,1,35,5,2,94,0
	db	47,1,1,1,82,2,1,86,2,3,85,3,1,61,3,2,35,3,3,58,4,1,17,4,2,129
	db	4,3,19,5,1,59,5,2,126,5,3,87,0

; rival
	db	43,1,1,3,89,2,4,134,3,4,73,4,1,58,5,1,109,5,4,83,6,3,98
	db	0
	db	43,2,1,3,89,2,4,134,3,4,73,4,1,85,5,1,58,6,2,115,6,3,98,0		
	db	43,3,1,3,89,2,4,134,3,4,73,4,1,109,4,4,83,5,1,85,6,1,62
	db	6,3,98,0

	db	0ffh


dealer_mons_tbl:
	include	..\data\dmons.sym
	include	..\data\dmons.dat




