
	include	common.def
	include	fntequ.def
	include	maphead.def
	include	banktool.def
	include	macro.h
	include	group.def


bank11	group	G_BANK11


	public	d10_room1_11f


	extern	bank_push_call
	extern	bank2bank
	extern	set_data

	extern	put_wait
	extern	elev_effect



DEPART_TYPE	equ	18
EXIT		=	0ffh


d10_room1_11f:
ysiz	=	2
xsiz	=	2
	db	DEPART_TYPE		; map type
	db	ysiz,xsiz		; yx size
	dw	d10depelv		; map data adrs
	dw	msg_d10r1f11		; message tbl adrs
	dw	ev_d10r1f11		; event routine adrs
	db	0			; map link flg
	dw	sxy_d10r1f11


ev_d10r1f11:
	ld	hl,ptn_data
	bit	5,(hl)
	res	5,(hl)
	push	hl
	call	nz,init_elevator$
	pop	hl
	bit	7,(hl)
	res	7,(hl)
	call	nz,effect

	xor	a
	ld	(win_cancel),a
	inc	a
	ld	(wait_pass_flg),a

	ret


init_elevator$:
	ld	hl,ie_tbl

	ld	a,(elevator + 0)
	ld	b,a
	ld	a,(elevator + 1)
	ld	c,a

	call	twice$

twice$:
	inc	hl
	inc	hl
	ld	a,b
	ld	(hli),a
	ld	a,c
	ld	(hli),a

	ret


init_elevator:
	ld	hl,d10_depart
	call	set_data

	ld	hl,stop_floor
	ld	de,anime_buf + 0
	ld	bc,floor_size
	call	block_move

	ret


d10_depart:
	db	11
	db	86				; 1F
	db	87
	db	88
	db	89
	db	90
	db	91
	db	92
	db	93
	db	94
	db	95
	db	96				; 11F
	db	0ffh


stop_floor:
	db	3,T11R6F1
	db	2,D10R1F1
	db	2,D10R1F2
	db	2,D10R1F3
	db	2,D10R1F4
	db	2,D10R1F5
	db	2,D10R1F6
	db	2,D10R1F7
	db	2,D10R1F8
	db	2,D10R1F9
	db	1,D10R1F10
floor_size	equ	* - stop_floor



effect:
	call	put_wait

	ld	b,01dh
	ld	hl,elev_effect
	call	bank_push_call

	ret



msg_d10r1f11:
	dw	msg1


msg1:
	db	CALL_MSG

	call	init_elevator

	ld	hl,stop_floor
	ld	a,97				; "elevator_system" in bank11
	call	bank2bank

	jp	msg_return


sxy_d10r1f11:
	include	..\sxy\d10r0111.sxy

d10depelv:
	include	..\mapdata\depelv.map

