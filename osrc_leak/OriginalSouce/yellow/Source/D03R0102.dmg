	include	common.def
	include	fntequ.def
	include	maphead.def
	include	banktool.def
	include	dealer.def
	include	vanish.def
	include	anime.def
	include	macro.h
	include	group.def


bank18	group	G_BANK18


	public	d3_room2


	extern	put_wait
	extern	musraibaru
	extern	direct_play
	extern	obj_stopping
	extern	table_jump
	extern	bank2bank
	extern	ex_yes_no
	extern	missing
	extern	set_battle
	extern	obs_battler
	extern	set_battle_msg
	extern	set_battle_data
	extern	actor_anime_set
	extern	save_position
	extern	load_position
	extern	obj_stepping
	extern	capsule_item
	extern	acttbl_l_adrs
	extern	mk_trace_cmd
	extern	CheckAssignPos
	extern	GetAnimeStatus
	extern	GetMoveStatus
	extern	SetActorSite

	extern	MusicStop

SHIP_TYPE	equ	13
EXIT		=	0ffh


seq_d3r1f2	equ		seq_ctrl_buf + 075h


HITOKAGE		equ	176
ZENIGAME		equ	177
FUSIGIDANE		equ	153


d3_room2:
ysiz	=	9
xsiz	=	20
	db	SHIP_TYPE	
	db	ysiz,xsiz		; yx size
	dw	dungeon3_2		; map data adrs
	dw	msg_d3r1f2  		; message tbl adrs
	dw	ev_d3r1f2     		; event routine adrs
	db	0			; map link flg
	dw	sxy_d3r1f2  


ev_d3r1f2:      
	call	init_for_talkmap

	ld	hl,d3r1f2_tbl
	ld	a,(seq_d3r1f2)
	jp	table_jump			; event routine address (hl)


return:
	xor	a
	ld	(cancel_key),a
	ld	(seq_d3r1f2),a

	ret


d3r1f2_tbl:
	initndw	matasitemo,seq_matasitemo
	ndw	step_in,seq_step_in
	ndw	after_battle,seq_after_battle
	ndw	step_out,seq_step_out
	ndw	free,seq_free


free:
	ret


matasitemo:
	ld	hl,rival_point
	call	CheckAssignPos
	ret	nc

	call	MusicStop

no_bank_change$:
	ld	c,MUSIC_BANK1_NO
	ld	a,< musraibaru
	call	direct_play

	ld	a,(usr_buf + 0)
	ld	(DA_BUF + 0),a

	ld	a,D3R1F2msg2
	ld	(obs_obj),a
	ld	a,B_REQ_OBJ_APPEAR
	call	bank2bank

	call	put_wait

	ld	a,msg_d_rival
	ld	(work1),a
	call	obj_stopping

	xor	a
	ld	(joy_status),a

	ld	a,0f0h
	ld	(cancel_key),a

	ld	a,(DA_BUF + 0)
	cp	2
	jr	nz,left$

	ld	de,near_step1$

	jr	set1$


left$:
	ld	de,near_step2$

set1$:
	call	actor_anime_set

	ld	a,seq_step_in
	ld	(seq_d3r1f2),a

	ret


near_step1$:
	db	A_D

near_step2$:
	db	A_D,A_D,A_D
	db	A_E


rival_point:
	db	008h,024h
	db	008h,025h
	db	0ffh



site_setting:
	ld	a,(mapscloll_x)
	cp	025h
	jr	nz,left1$

	ld	a,2
	ld	(watashi_muki),a

	ld	a,00ch

	jr	set2$


left1$:
	xor	a

set2$:
	ld	(work2),a
	ld	a,msg_d_rival
	ld	(work1),a
	jp	SetActorSite




step_in:
	ld	a,(obs_system)
	bit	0,a
	ret	nz

	call	site_setting

	xor	a
	ld	(cancel_key),a

	ld	a,msg_d_rival
	ld	(work1),a
	call	talk_map

	call	put_wait

	ld	a,d_rival2
	ld	(event_fight_no),a
	ld	a,01h
	ld	(take_mons_ptn),a

	call	site_setting

	ld	a,seq_after_battle
	ld	(seq_d3r1f2),a

	ret


after_battle:
	ld	a,(fighting_flg)
	cp	0ffh
	jp	z,return			; to okido school(losing)

	call	site_setting

	ld	a,0f0h
	ld	(cancel_key),a

	ld	a,msg_after
	ld	(work1),a
	call	talk_map

	ld	a,msg_d_rival
	ld	(work1),a
	call	obj_stopping

	ld	a,(mapscloll_x)
	cp	025h
	jr	nz,left3$

	ld	de,near_out2$

	jr	set4$


left3$:
	ld	de,near_out1$

set4$:
	ld	a,msg_d_rival
	ld	(work1),a
	call	actor_anime_set

	call	MusicStop

	ld	b,2
	extern	set_rival_leave_music
	ld	hl,set_rival_leave_music
	extern	bank_push_call
	call	bank_push_call

	ld	a,seq_step_out
	ld	(seq_d3r1f2),a

	ret


near_out1$:
	db	A_R,A_D

near_out2$:
	db	A_D,A_D,A_D,A_D
	db	A_E


step_out:
	ld	a,(obs_system)
	bit	0,a
	ret	nz

	xor	a
	ld	(cancel_key),a

	ld	a,D3R1F2msg2
	ld	(obs_obj),a
	ld	a,B_REQ_OBJ_VANISH
	call	bank2bank

	extern	set_now_music
	call	set_now_music

	ld	a,seq_free
	ld	(seq_d3r1f2),a

	ret



msg_d3r1f2:   
	msgndw	msg1,msg_boy1
	ndw	msg2,msg_d_rival
	ndw	msg2_after,msg_after


msg1:
	extern	msg1_0_D03R0102
	db I_MSG2	; mvmsg追加
	dw msg1_0_D03R0102	; mvmsg追加
	db 026h	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg2:
	db	CALL_MSG

	ld	hl,msg2_battle_in
	call	put_win_msg

	ld	hl,event_flg
	set	6,(hl)
	set	7,(hl)

	ld	hl,msg2_win			; hero lose
	ld	de,msg2_lose			; hero win
	call	set_battle_msg

	jp	msg_return


msg2_battle_in:
	extern	msg2_battle_in_1_D03R0102
	db I_MSG2	; mvmsg追加
	dw msg2_battle_in_1_D03R0102	; mvmsg追加
	db 026h	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg2_win:
	extern	msg2_win_2_D03R0102
	db I_MSG2	; mvmsg追加
	dw msg2_win_2_D03R0102	; mvmsg追加
	db 026h	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg2_lose:
	extern	msg2_lose_3_D03R0102
	db I_MSG2	; mvmsg追加
	dw msg2_lose_3_D03R0102	; mvmsg追加
	db 026h	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg2_after:
	extern	msg2_after_4_D03R0102
	db I_MSG2	; mvmsg追加
	dw msg2_after_4_D03R0102	; mvmsg追加
	db 026h	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加



sxy_d3r1f2:   
	include	..\sxy\d03r0102.sxy


dungeon3_2:
	include	..\mapdata\stannu02.map   






