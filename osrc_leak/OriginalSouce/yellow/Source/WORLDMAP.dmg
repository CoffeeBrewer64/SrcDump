
	include	common.def
	include	fntequ.def
	include	banktool.def
	include	sgb_col.def
	include	anime.def
	include	fight.def
	include macro.h
	include	group.def
	include	pm_debug.def

;	extern	objct
;	extern	objct6
;	extern	komono
	include	objbank.def


bank1c	group	G_BANK1c

	public	worldmap
	public	worldmap2
	public	distribution
	public	w_map_effect
	public	allow_effect
	public	allow_effect2
	public	blue_special

	extern	lcdc_stop
	extern	cap_list
	extern	lcdc_on
	extern	cont_abwait
	extern	cont_repeat
	extern	memset
	extern	mul_any
	extern	str_cpy
	extern	se_wait
	extern	set_objdata
	extern	block_move
	extern	oam_clr
	extern	step_prn_win
	extern	actor_blanch
	extern	fnt_world
	extern	fnt_serifu
	extern	set_serifu
;;	extern	raplus
	extern	chrset
	extern	chrmove
	extern	fontmove
	extern	wait_vb
	extern	wait_vb_s
	extern	put_window
	extern	put_map
	extern	put_msg
	extern	put_win_msg
	extern	put_all
	extern	put_wait
	extern	put_1cell
	extern	puthaji_yoko
	extern	ready2ready
	extern	bank2bank
	extern	bank_push_call
	extern	dvram_cls
	extern	color_set
	extern	fontset
	extern	color_rewrite
	extern	palset
	extern	pal_off
	extern	pal_off_put_wait
	extern	color_reset
	extern	set_oam_buf16
	extern	set_jiki
	extern	set_kana
	extern	set_gauge
	extern	set_objects
	extern	encount_tbl
	extern	encount
	extern	init_map
	extern	sub_capsule
	extern	add_capsule_new
	extern	musfanfare5
	extern	get_monsadr
	extern	prt_mons_chr
	extern	map_rewrite
	extern	gyaarth
	extern	gyaarth_play
	extern	get_mons_name
	extern	yes_no
	extern	iai_effect
	extern	townimg
	extern	get_pet_name
	extern	ef1
	extern	push_vram
	extern	push_vram_m
	extern	pop_vram
	extern	pop_vram_m
	extern	block_cls

	extern	muswngo
	extern	musshinka
	extern	muscha
	extern	direct_play
	extern	set_now_music

	extern	shinka_0

	extern	BGMplay
	extern	SEplay
	extern	MusicStop

	extern	change_cgb_obp1

;====================================================;
;	WORLDMAP			             ;
;                                                    ;
;====================================================;
worldmap:
	call	wmap_put		; ポケモン地図スクリーンデータを描画する

	ld	hl,oam_flg
	ld	a,(hl)
	push	af			; push	(oam_flg)
	ld	(hl),0ffh		; (oam_flg) = $FF
	push	hl

	ld	a,1
	ld	(cont_joy_flg),a	; (cont_joy_flg) = 1

	ld	a,(mapno)
	push	af			; push	(mapno)

	ld	b,0
	call	myplace_set
 ifn	ASSEMBLE__ENGLISH
	S_POS	1,0
 else
	ld	hl,dmy_vram + 21	; S_POS(1,1)
 endif
	ld	de,table_data
	call	put_msg
	
	ld	hl,oam_buf
	ld	de,dmy_map
	ld	bc,4*4
	call	block_move

	ld	hl,4*16 + 8000h
	ld	de,cursule$		; 【】カーソルのキャラクタデータ
	ld	bc,1ch*100h + 4
	call	fontmove

	xor	a
	ld	(usr_buf),a
	pop	af
	jr	next$

loop$:
	ld	hl,dmy_vram 			; 地名表示部分を spc@ クリアする
 ifn	ASSEMBLE__ENGLISH
 	ld	bc,0114h			; S_POS(0,0)〜(0,19) をクリアする
 else
 	ld	bc,020ah			; S_POS(0,0)〜(1,9) をクリアする
 endif
	call	block_cls			; block_cls (HL dvram, B y, C x)

	ld	hl,place_tbl$
	ld	a,(usr_buf)
	ld	c,a
	ld	b,0
	add	hl,bc

	ld	a,(hl)
next$:
	ld	de,yes_no_map
	call	placeset_main
	ld	a,(de)
	push	hl

	call	place_set
	ld	a,4
	ld	(usr_buf + FREE_AREA_@),a	; ld	(usr_buf + 20),a
	ld	hl,4*4 + oam_buf
	call	oam_chrset

	pop	hl

	ld	de,table_data
loop1$:
	ld	a,(hli)
	ld	(de),a
	inc	de
	cp	EOM
	jr	nz,loop1$

 ifn	ASSEMBLE__ENGLISH
	S_POS	1,0
 else
	ld	hl,dmy_vram + 21		; S_POS(1,1)
 endif
	ld	de,table_data
	call	put_msg

	ld	hl,4*4 + oam_buf
	ld	de,4*4 + dmy_map
	ld	bc,4*4
	call	block_move

cont_loop$:
	call	w_map_effect
	call	cont_repeat

	ld	a,(joy_repeat)
	ld	b,a
	and	0c3h
	jr	z,cont_loop$

	ld	a,muswngo
	call	SEplay

	bit	6,b				; Check 'U' Button
	jr	nz,up$
	bit	7,b				; Check 'D' Button
	jr	nz,down$

	xor	a
	ld	(ef_adr4_l),a		;worldmap_flg res
	ld	(cont_joy_flg),a
	ld	(ef_count2),a
	call	map_exit
	pop	hl
	pop	af
	ld	(hl),a

	ret

up$:
	ld	a,(usr_buf)
	inc	a
	cp	47
	jr	nz,n1$
	xor	a
n1$:
	ld	(usr_buf),a
	jp	loop$
	
down$:
	ld	a,(usr_buf)
	dec	a
	cp	0ffh
	jr	nz,n2$
	ld	a,46
n2$:
	ld	(usr_buf),a
	jp	loop$
	

	extern	PikaVoicePlay
bitplay_test$:
	ld	a,(joy_repeat)
	and	BT_U+BT_D
	ret	z

	ld	hl,PikaVoicePlay
	ld	b,BANK__PikaVoicePlay	;	ld	b,3bh
	call	bank_push_call
	ret


place_tbl$:
	db	0 ;	masara$
	db	12 ;	road1$
	db	1 ;	tokiwa$
	db	13 ;	road2$
	db	51 ;	mori$
	db	197 ;	digda$
	db	2 ;	nibi$
	db	14 ;	road3$
	db	59 ;	otsukimi$
	db	15 ;	road4$
	db	3 ;	hanada$
	db	35 ;	road24$
	db	36 ;	road25$
	db	88 ;	masaki$
	db	16 ;	road5$
	db	17 ;	road6$
	db	5 ;	kutiba$
	db	95 ;	santoannu$
	db	20 ;	road9$
	db	81 ;	iwayama$
	db	21 ;	road10$
	db	4 ;	shion$
	db	143 ;	tower$
	db	19 ;	road8$
	db	18 ;	road7$
	db	6 ;	tamamushi$
	db	10 ;	yamabuki$
	db	22 ;	road11$
	db	23 ;	road12$
	db	24 ;	road13$
	db	25 ;	road14$
	db	26 ;	road15$
	db	27 ;	road16$
	db	28 ;	road17$
	db	29 ;	road18$
	db	7 ;	sekitiku$
	db	217 ;	safari$
	db	30 ;	road19$
	db	159 ;	freezer$
	db	31 ;	road20$
	db	8 ;	guren$
	db	32 ;	road21$
	db	33 ;	road22$
	db	34 ;	road23$
	db	198 ;	champ$
	db	9 ;	sekiei$
	db	83 ;	muzin$

cursule$:
	db	0feh,0feh,0c0h,0c0h,0c0h,0c0h,0c0h,000h
	db	07fh,07fh,003h,003h,003h,003h,003h,000h
	db	000h,0c0h,0c0h,0c0h,0c0h,0c0h,0feh,0feh
	db	000h,003h,003h,003h,003h,003h,07fh,07fh

;====================================================;
;	MONSTER DISTRIBUTION MAP                     ;
;                                                    ;
;====================================================;
distribution:
	call	wmap_put

	ld	hl,oam_flg
	ld	a,(hl)
	push	af
	ld	(hl),0ffh
	push	hl

	call	distribution_m
	call	get_mons_name			; return  DE= table_data
 ifn  pm_jmsg
	ld	hl,dmy_vram + 20		; ポケモン名を表示  S_POS(0,1)
 else
	S_POS	1,0
 endif
	call	put_msg
 ifn	ASSEMBLE__ENGLISH
  ifn  pm_jmsg
  else
	ld	h,b
	ld	l,c
	ld	de,msg$				; "'s NEST"
	call	put_msg
  endif
 else
	ld	hl,dmy_vram + 25
	ld	de,msg$				; ”の すみか”を表示
	call	put_msg
 endif
	call	cont_abwait

	call	map_exit

	pop	hl
	pop	af
	ld	(hl),a

	ret

 ifn  pm_jmsg	
msg$:
;	db	ha_,spc@,ko_,ko_,da_,EOM
	db	no_,spc@,su_,mi_,ka_,EOM	; ”の すみか”
 else
msg$:
	db	apt_s_,spc@,usf_n,usf_e,usf_s,usf_t,EOM		; 's NEST
 endif

;====================================================;
;	WORLDMAP2			             ;
;                                                    ;
;====================================================;

UP_ALLOW_XPOS	=	18	; ▲カーソルの S_POS Ｘ座標値
DOWN_ALLOW_XPOS	=	19	; ▼カーソルの S_POS Ｘ座標値
UP_DOWN_ALLOW_XPOS	= UP_ALLOW_XPOS

worldmap2:
	call	oam_clr
	call	wmap_put

	ld	a,1
	ld	(cont_joy_flg),a

	call	set_jiki
	call	set_kana
;	ld	de,objct + 0c00h
;	ld	bc,OBJ_CHARA_BANK1*100h + 12		; bank number + counter
	ld	de,OBJ_TORI
	ld	b,CBANK_TORI
	ld	c,12
	ld	hl,8040h
	call	chrmove			; vram set

	ld	de,allowu		; ▲ カーソルのキャラクタ
	ld	hl,allow@*10h + 8000h
	ld	bc,1ch*256 + 1		;Bank No + Position Y Counter
	call	fontmove
	call	town_chk

	ld	hl,oam_flg
	ld	a,(hl)
	push	af
	ld	(hl),0ffh
	push	hl

 ifn  pm_jmsg
	ld	hl,dmy_vram + 25	; S_POS(5,1)〜 ”へ とぶ”を表示する
 else
	S_POS	0,0			; "To"
 endif
	ld	de,msg$
	call	put_msg

	ld	a,(mapno)
	ld	b,0
	call	myplace_set

	ld	hl,usr_buf + 1
 ifn  pm_jmsg
	ld	de,dmy_vram + 29	; S_POS(9,1)
 else
;	ld	de,dmy_vram +20 + UP_DOWN_ALLOW_XPOS	; S_POS(UP_DOWN_ALLOW_XPOS,1)
	ld	de,dmy_vram + UP_ALLOW_XPOS		; S_POS(UP_DOWN_ALLOW_XPOS,0)
 endif
loop$:
	ld	a,spc@
;	ld	(de),a			; S_POS(9,1) or S_POS(9,0) = spc@
	ld	(de),a			; S_POS(18,0) or S_POS(19,0) = spc@
	push	hl
	push	hl

 ifn  pm_jmsg
	ld	hl,dmy_vram + 1		; S_POS(1,0)〜(3,0)= spc@
	ld	de,msg1$
	call	put_msg
	ld	hl,dmy_vram + 23	; S_POS(3,1)
	ld	a,spc@
	ld	(hli),a
	ld	(hl),a			; S_POS(3,1)= S_POS(4,1)= spc@
 else
	S_POS	3,0
	ld	bc,1 *100h + UP_DOWN_ALLOW_XPOS -3	; 地名表示部分をクリアする
	call	block_cls
 endif

	pop	hl
	ld	a,(hl)
	ld	b,4
	call	myplace_set
 ifn  pm_jmsg
	ld	hl,dmy_vram + 21	; S_POS(1,1)〜	地名を表示する
 else
	S_POS	3,0
 endif
	ld	de,table_data
	call	put_msg

	ld	c,15
	call	wait_vb_s

 ifn  pm_jmsg
	S_POS	9,0
	ld	(hl),allow@		; ▲カーソルを表示
	S_POS	9,1
	ld	(hl),allowd@		; ▼カーソルを表示
 else
	S_POS	UP_ALLOW_XPOS,0
	ld	(hl),allow@
	S_POS	DOWN_ALLOW_XPOS,0
	ld	(hl),allowd@
 endif

	pop	hl			; HL = &usr_buf[1]
cont_loop$:
	push	hl
	call	wait_vb
	call	cont_repeat
	ld	a,(joy_repeat)
	ld	b,a
	pop	hl

	and	0c3h
	jr	z,cont_loop$

	bit	0,b				; Check 'A' Button
	jr	nz,next$

	ld	a,muswngo
	call	SEplay

	bit	6,b				; Check 'U' Button
	jr	nz,up$

	bit	7,b				; Check 'D' Button
	jr	nz,down$

	jr	exit$				; Check 'B' Button


next$:
;	ld	a,muscha
	extern	musitemuse
	ld	a,musitemuse
	call	SEplay

	ld	a,(hl)
	ld	(ev_something + 2),a

	ld	hl,game_mode
	set	3,(hl)
	inc	hl
	set	7,(hl)
exit$:
	xor	a
	ld	(ef_adr4_l),a		;worldmap_flg res
	ld	(cont_joy_flg),a

	call	pal_off_put_wait
;	call	dvram_cls
;	call	oam_clr

;	call	actor_blanch
;	call	color_rewrite
;	call	map_rewrite

	pop	hl
	pop	af
	ld	(hl),a
	ret


up$:
 ifn  pm_jmsg
	ld	de,dmy_vram + 9				; S_POS(9,0)
 else
	ld	de,dmy_vram + UP_ALLOW_XPOS		; S_POS(UP_ALLOW_XPOS,0)
 endif
	inc	hl
	ld	a,(hl)
	cp	0ffh
	jr	z,up_end$
	cp	0feh
	jr	z,up$
	jp	loop$
	
up_end$:
	ld	hl,usr_buf + 1
	jp	loop$
	
down$:
 ifn  pm_jmsg
	ld	de,dmy_vram + 29			; S_POS(9,1)
 else
	ld	de,dmy_vram + DOWN_ALLOW_XPOS		; S_POS(DOWN_ALLOW_XPOS,0)
 endif
	dec	hl
	ld	a,(hl)
	cp	0ffh
	jr	z,down_end$
	cp	0feh
	jr	z,down$
	jp	loop$
	
down_end$:
	ld	hl,usr_buf + 12
	jr	down$
	
	
 ifn  pm_jmsg	
msg$:	db	he_,spc@,to_,bu_,EOM		; へ とぶ
msg1$:	db	spc@,spc@,spc@,spc@,EOM
 else
msg$:	db	usf_t,usf_o_,EOM		; "To"
 endif


town_chk:
	ld	hl,usr_buf
	ld	(hl),0ffh
	inc	hl

	ld	a,(save_event_flg + 0)
	ld	e,a
	ld	a,(save_event_flg + 1)
	ld	d,a

	ld	bc,11
loop$:
	srl	d
	rr	e
	ld	a,0feh
	jr	nc,z1$
	ld	a,b
z1$:
	ld	(hl),a
	inc	hl
	inc	b
	dec	c
	jr	nz,loop$

	ld	(hl),0ffh
	
	ret

allowu:
	db	000h,000h,010h,038h	; ▲ カーソル
	db	07Ch,0FEh,0FEh,000h

;====================================================;
;	WORLDMAP PUT                                 ;
;                                                    ;
;====================================================;
wmap_put:
	call	pal_off_put_wait
	call	dvram_cls
	call	actor_blanch

	SET_WIN_POS 0,0,19,19
	call	put_window

	call	lcdc_stop

	ld	hl,fnt_world
	ld	de,09600h
	ld	bc,16*16
	ld	a,4
	call	chrset

	ld	hl,mapmons
	ld	de,08040h
	ld	bc,1*8
	ld	a,1ch
	call	fontset

	ld	hl,dmy_vram
	ld	de,worldmap_tbl$
loop1$:
	ld	a,(de)		; (DE) 上位４ビット：キャラクタ番号下位桁（上位桁は６０ｈ固定）
	and	a		;      下位４ビット：繰り返し個数
	jr	z,exit$

	ld	b,a		;c->count a-> chr_no
	and	00fh
	ld	c,a
	ld	a,b
	swap	a
	and	00fh
	add	a,060h
loop$:	
	ld	(hli),a
	dec	c
	jr	nz,loop$

	inc	de
	jr	loop1$
exit$:
	call	lcdc_on
	ld	b,COL_WMAP
	call	color_set

	call	put_wait
	call	palset

	xor	a
	ld	(ef_count2),a
	inc	a
	ld	(ef_adr4_l),a		;worldmap_flg set
	ret

; ポケモン地図スクリーンデータ	（２０＊１８）
;	上位４ビット：キャラクタ番号下位桁（上位桁は６０ｈ固定）
;	下位４ビット：繰り返し個数
 ifn	ASSEMBLE__ENGLISH
worldmap_tbl$:		; ポケモン地図スクリーンデータ（英語版）
	db	07fh,075h								;1
	db	06ch,073h,081h,044h							;2
	db	06ch,071h,064h,081h,042h						;3
	db	062h,051h,064h,071h,0c1h,073h,051h,074h,062h,041h			;4
	db	062h,071h,061h,051h,073h,064h,071h,063h,0c1h,061h,0b1h,041h		;5
	db	062h,0c1h,061h,0c1h,067h,071h,063h,071h,0b1h,042h			;6
	db	062h,071h,061h,071h,061h,073h,051h,072h,051h,073h,051h,043h		;7
	db	062h,071h,061h,071h,061h,071h,065h,071h,063h,071h,043h			;8
	db	062h,071h,061h,071h,061h,071h,0b1h,0a1h,063h,071h,063h,071h,043h	;9
	db	062h,072h,051h,061h,071h,043h,0a1h,061h,071h,063h,071h,043h		;10
	db	061h,0b1h,0a1h,061h,071h,061h,071h,061h,043h,061h,051h,074h,091h,042h	;11
	db	0b1h,042h,061h,071h,061h,071h,0b1h,043h,0a1h,064h,071h,0b1h,042h	;12
	db	043h,061h,051h,0b1h,0f1h,045h,061h,074h,043h				;13
	db	043h,0a1h,071h,041h,0f1h,042h,091h,063h,071h,062h,044h			;14
	db	044h,0f1h,041h,0d1h,0e1h,072h,051h,073h,061h,0b1h,044h			;15
	db	043h,091h,071h,081h,042h,0a1h,061h,071h,0b1h,048h			;16
	db	043h,061h,051h,071h,0e1h,0c1h,0e2h,0d1h,049h				;17
	db	04fh,045h								;18
	db	0
 else
worldmap_tbl$:		; ポケモン地図スクリーンデータ
	db	07ah,031h,028h,031h				;1
	db	07ah,011h,061h,073h,081h,043h,011h		;2
	db	031h,029h,031h,061h,071h,064h,081h,041h,011h	;3
	db	011h,061h,051h,064h,071h,0c1h,073h,051h,074h	;4
	db	062h,011h						
	db	011h,061h,071h,061h,051h,073h,064h,071h,063h	;5
	db	0c1h,061h,0b1h,011h
	db	011h,061h,0c1h,061h,0c1h,067h,071h,063h,071h	;6
	db	0b1h,041h,011h
	db	011h,061h,071h,061h,071h,061h,073h,051h,072h	;7
	db	051h,073h,051h,042h,011h
	db	011h,061h,071h,061h,071h,061h,071h,065h,071h	;8
	db	063h,071h,042h,011h
	db	011h,061h,071h,061h,071h,061h,071h,0b1h,0a1h	;9
	db	063h,071h,063h,071h,042h,011h
	db	011h,061h,072h,051h,061h,071h,043h,0a1h,061h	;10
	db	071h,063h,071h,042h,011h
	db	011h,0b1h,0a1h,061h,071h,061h,071h,061h,043h	;11
	db	061h,051h,074h,091h,041h,011h
	db	011h,042h,061h,071h,061h,071h,0b1h,043h,0a1h	;12
	db	064h,071h,0b1h,041h,011h
	db	011h,042h,061h,051h,0b1h,0f1h,045h,061h,074h	;13
	db	042h,011h
	db	011h,042h,0a1h,071h,041h,0f1h,042h,091h,063h	;14
	db	071h,062h,043h,011h
	db	011h,043h,0f1h,041h,0d1h,0e1h,072h,051h,073h	;15
	db	061h,0b1h,043h,011h
	db	011h,042h,091h,071h,081h,042h,0a1h,061h,071h	;16
	db	0b1h,047h,011h
	db	011h,042h,061h,051h,071h,0e1h,0c1h,0e2h,0d1h	;17
	db	048h,011h
	db	031h,02fh,023h,031h				;18
	db	0
 endif

;====================================================;
;	MAP_EXIT  		                     ;
;       rewrite  : kana,obj,color		     ;
;====================================================;
map_exit:
	xor	a
	ld	(ef_adr4_l),a		;worldmap_flg res

	call	pal_off
	call	dvram_cls
	call	oam_clr

	call	set_jiki
	call	set_kana
	
	call	actor_blanch
	jp	color_rewrite

;====================================================;
;	place_set  in   map_no                       ;
;                                                    ;
;====================================================;
myplace_set:
	push	af
	ld	a,b
	ld	(usr_buf + FREE_AREA_@),a	; ld	(usr_buf + 20),a
	pop	af

	ld	de,yes_no_map		; placeset_main(DE position_save adrs, (mapno))
	call	placeset_main		;	return (DE)=position, HL=position_name adrs
	ld	a,(de)
	push	hl

	call	place_set		; place_set(A YXpos, HL buf) return buf[0]=Ypos,buf[1]=Xpos
	call	jiki_set

	pop	hl

	ld	de,table_data
loop$:
	ld	a,(hli)			; HL= position_name adrs （地名データ）
	ld	(de),a			; table_data[] = 地名データ
	inc	de
	cp	EOM
	jr	nz,loop$

	ld	hl,oam_buf
	ld	de,dmy_map
	ld	bc,40*4
	jp	block_move


distribution_m:
	ld	b,3
	ld	hl,distribution_set
	call	bank_push_call

	call	same_chk

	ld	hl,oam_buf
	ld	de,yes_no_map
loop1$:
	ld	a,(de)
	cp	0ffh
	jr	z,end2$

	and	a
	jr	z,next$
	
	push	hl
	call	placeset_main
	pop	hl
	ld	a,(de)
	cp	019h		;D13 wasurerareta_doukutu
	jr	z,next$

	call	place_set
	ld	a,4
	ld	(hli),a
	xor	a
	ld	(hli),a
next$:
	inc	de
	jr	loop1$
	
end2$:	
	ld	a,l
	and	a
	jr	nz,end3$

	SET_WIN_POS	1,7,17,10
	call	put_window
	
	S_POS	2,9
	ld	de,no_here_msg$
	call	put_msg
	jr	exit$
	
end3$:	
	ld	a,(mapno)
	ld	b,0
	call	myplace_set
exit$:
	ld	hl,oam_buf
	ld	de,dmy_map
	ld	bc,40*4
	jp	block_move

no_here_msg$:
  ifn	pm_jmsg
	db	spc@,se_,i_,so_,ku_,ti_,spc@,hu_,me_,i_
	db	EOM
  else
	db	spc@,usf_a,usf_r,usf_e,usf_a,spc@,usf_u,usf_n,usf_k,usf_n,usf_o,usf_w,usf_n
	db	EOM
  endif


place_set:				; place_set(A position)
	push	af			; A= 上位４ビット：Ｙ座標、下位４ビット：Ｘ座標
	and	0f0h
	srl	a
; ifn	ASSEMBLE__ENGLISH
;	add	a,020h			; a= (position<H>)/2 + $20 （地図を１キャラ下へ下げた為）
; else
	add	a,018h			; a= (position<H>)/2 + $18
; endif
	ld	b,a			;b = Yposition
	ld	(hli),a

	pop	af
	and	00fh
	swap	a
	srl	a
	add	a,018h			; a= (position<L>)/2 + $18
	ld	c,a			;c = Xposition
	ld	(hli),a
	ret


jiki_set:
	ld	a,(usr_buf + FREE_AREA_@)	; ld	a,(usr_buf + 20)
	and	a
	ld	hl,36*4 + oam_buf
	jr	z,oam_chrset
	ld	hl,32*4 + oam_buf

oam_chrset:
	push	hl
	ld	hl,0fcfch		;b,c -> -4
	add	hl,bc
	ld	b,h
	ld	c,l
	pop	hl

oam_chrset2:
	ld	de,0202h
loop1$:
	push	de
	push	bc
loop$:
	ld	a,b
	ld	(hli),a
	ld	a,c
	ld	(hli),a
	ld	a,(usr_buf + FREE_AREA_@)	; ld	a,(usr_buf + 20)
	ld	(hli),a
	inc	a
	ld	(usr_buf + FREE_AREA_@),a	; ld	(usr_buf + 20),a
	xor	a
	ld	(hli),a
	inc	d
	ld	a,8
	add	a,c
	ld	c,a
	dec	e
	jr	nz,loop$
	pop	bc
	pop	de
	ld	a,8
	add	a,b
	ld	b,a
	dec	d
	jr	nz,loop1$
	ret
	
oam_chrset3:
	xor	a
	ld	(usr_buf + FREE_AREA_@ +1),a	; ld	(usr_buf + 21),a
	
	ld	de,0202h
loop1$:
	push	de
	push	bc
loop$:
	ld	a,b
	ld	(hli),a
	ld	a,c
	ld	(hli),a
	ld	a,(usr_buf + FREE_AREA_@)	; ld	a,(usr_buf + 20)
	ld	(hli),a
	ld	a,(usr_buf + FREE_AREA_@ +1)	; ld	a,(usr_buf + 21)
	ld	(hli),a
	xor	20h
	ld	(usr_buf + FREE_AREA_@ +1),a	; ld	(usr_buf + 21),a
	inc	d
	ld	a,8
	add	a,c
	ld	c,a
	dec	e
	jr	nz,loop$
	pop	bc
	pop	de

	push	hl
	ld	hl,usr_buf + FREE_AREA_@	; ld	hl,usr_buf + 20
	inc	(hl)
	inc	(hl)
	pop	hl
	
	ld	a,8
	add	a,b
	ld	b,a
	dec	d
	jr	nz,loop1$
	ret
	
	
same_chk:
	ld	de,yes_no_map
loop$:
	ld	a,(de)
	inc	de
	cp	0ffh
	ret	z

	ld	c,a
	ld	l,e
	ld	h,d
loop2$:
	ld	a,(hl)
	cp	0ffh
	jr	z,loop$
	
	cp	c
	jr	nz,next$
	xor	a
	ld	(hl),a
next$:
	inc	hl
	jr	loop2$

;================================================================;
;   PLACESET_MAIN                                                ;
;       IN    :    mapno   0~255                                 ;
;             :    possition_save address -> DE                  ;
;       OUT   :    possition              -> (DE)                ;
;             :    possition_name address -> HL                  ;
;================================================================;
placeset_main:
	cp	025h
	jr	c,town$

	ld	bc,4
	ld	hl,otherplace_tbl$
loop$:
	cp	(hl)
	jr	c,hit$
	add	hl,bc
	jr	loop$
hit$:
	inc	hl
	jr	next$
town$:
	ld	hl,townplace_tbl$
	ld	c,a
	ld	b,0
	add	hl,bc
	add	hl,bc
	add	hl,bc
next$:
	ld	a,(hli)
	ld	(de),a
	ld	a,(hli)
	ld	h,(hl)
	ld	l,a
	ret
	

townplace_tbl$:
	db	0b2h
	dw	masara$
	db	082h
	dw	tokiwa$
	db	032h
	dw	nibi$
	db	02ah
	dw	hanada$
	db	05eh
	dw	shion$
	db	09ah
	dw	kutiba$
	db	057h
	dw	tamamushi$
	db	0d8h
	dw	sekitiku$
	db	0f2h
	dw	guren$
	db	020h
	dw	sekiei$
	db	05ah
	dw	yamabuki$

	db	000h
	dw	dummy$

	db	0a2h
	dw	road1$
	db	062h
	dw	road2$
	db	034h
	dw	road3$
	db	028h
	dw	road4$
	db	03ah
	dw	road5$
	db	08ah
	dw	road6$
	db	058h
	dw	road7$
	db	05dh
	dw	road8$
	db	02dh
	dw	road9$
	db	04eh
	dw	road10$
	db	09ch
	dw	road11$
	db	09eh
	dw	road12$
	db	0bdh
	dw	road13$
	db	0cbh
	dw	road14$
	db	0dah
	dw	road15$
	db	055h
	dw	road16$
	db	084h
	dw	road17$
	db	0d6h
	dw	road18$
	db	0f6h
	dw	road19$
	db	0f4h
	dw	road20$
	db	0d2h
	dw	road21$
	db	080h
	dw	road22$
	db	060h
	dw	road23$
	db	01ah
	dw	road24$
	db	00bh
	dw	road25$

otherplace_tbl$:
	db	41,0b2h		;T1
	dw	masara$
	db	46,082h		;T2
	dw	tokiwa$
	db	51,062h		;R2
	dw	road2$
	db	52,042h		;D1
	dw	mori$
	db	59,032h		;T3
	dw	nibi$
	db	62,026h		;D2
	dw	otsukimi$
	db	68,02ah		;T4
	dw	hanada$
	db	69,025h		;R4
	dw	road4$
	db	70,02ah		;T4
	dw	hanada$
	db	73,04ah		;R5
	dw	road5$
	db	76,06ah		;R6
	dw	road6$
	db	79,059h		;R7
	dw	road7$
	db	81,05bh		;R8
	dw	road8$
	db	83,03eh		;R10
	dw	iwayama$
	db	84,04fh		;R10
	dw	muzin$
	db	87,09dh		;R11
	dw	road11$
	db	88,07eh		;R12	
	dw	road12$
	db	89,00ch		;R25	
	dw	masaki$
	db	95,09ah		;T6
	dw	kutiba$
	db	105,0a9h	;D3
	dw	santoannu$
	db	109,040h	;R23
	dw	champ$
	db	119,020h	;D14
	dw	leage$
	db	120,05ah	;D4
	dw	tikatuuro$
	db	121,020h	;D14
	dw	leage$
	db	122,05ah	;D6
	dw	tikatuuro$
	db	141,057h	;T7
	dw	tamamushi$
	db	142,05eh	;T5
	dw	shion$
	db	149,05fh	;T5
	dw	tower$
	db	152,05eh	;T5
	dw	shion$
	db	156,0d8h	;T8
	dw	sekitiku$
	db	157,0c8h	;T8
	dw	safari$
	db	159,0d8h	;T8
	dw	sekitiku$
	db	163,0f5h	;D16
	dw	freezer$
	db	164,09ah	;T8
	dw	kutiba$
	db	165,0d8h	;T8
	dw	sekitiku$
	db	166,0f2h	;T9
	dw	yashiki$
	db	174,0f2h	;T9
	dw	guren$
	db	175,020h	;T10
	dw	sekiei$
	db	184,05ah	;T11
	dw	yamabuki$
	db	186,0d9h	;R15
	dw	road15$
	db	189,054h	;R16
	dw	road16$
	db	190,0aeh	;R16
	dw	road12$
	db	192,0d7h	;R18
	dw	road18$
	db	193,0f5h	;R20
	dw	freezer$
	db	194,070h	;R22
	dw	road22$
	db	195,040h	;R23
	dw	champ$
	db	196,07eh	;R12	
	dw	road12$
	db	197,09ah	;T6
	dw	kutiba$
	db	198,043h	;D7
	dw	digda$
	db	199,040h	;D8
	dw	champ$
	db	207,057h	;D9
	dw	rocket$
	db	214,05ah	;D10
	dw	sylpu$
	db	217,0f2h	;D11
	dw	yashiki$
	db	226,0c8h	;D12
	dw	safari$
	db	229,019h	;D13
	dw	wasure$
	db	230,05eh	;T4
	dw	shion$
	db	231,02ah	;T4
	dw	hanada$
	db	233,03eh	;D15
	dw	iwayama$
	db	237,05ah	;D10
	dw	sylpu$
	db	248,020h	;D10
	dw	leage$
  ifn	1			; add by tama,98/04/10
  	db	248+1,0f6h	;R19R1F1
	dw	road19$
  endif
	db	0ffh		; table end


dummy$:
masara$:
  ifn	pm_jmsg
	db	ma__,sa__,ra__,EOM
  else
	db	usf_p,usf_a,usf_l,usf_l,usf_e,usf_t,spc@,usf_t,usf_o,usf_w,usf_n,EOM
  endif

tokiwa$:
  ifn	pm_jmsg
	db	to__,ki__,wa__,EOM
  else
	db	usf_v,usf_i,usf_r,usf_i,usf_d,usf_i,usf_a,usf_n,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

nibi$:
  ifn	pm_jmsg
	db	ni__,bi__,EOM
  else
	db	usf_p,usf_e,usf_w,usf_t,usf_e,usf_r,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

hanada$:
  ifn	pm_jmsg
	db	ha__,na__,da__,EOM
  else
	db	usf_c,usf_e,usf_r,usf_u,usf_l,usf_e,usf_a,usf_n,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

shion$:
  ifn	pm_jmsg
	db	si__,o__,n__,EOM
  else
	db	usf_l,usf_a,usf_v,usf_e,usf_n,usf_d,usf_e,usf_r,spc@,usf_t,usf_o,usf_w,usf_n,EOM
  endif

kutiba$:
  ifn	pm_jmsg
	db	ku__,ti__,ba__,EOM
  else
	db	usf_v,usf_e,usf_r,usf_m,usf_i,usf_l,usf_i,usf_o,usf_n,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

tamamushi$:
  ifn	pm_jmsg
	db	ta__,ma__,mu__,si__,EOM
  else
	db	usf_c,usf_e,usf_l,usf_a,usf_d,usf_o,usf_n,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

sekitiku$:
  ifn	pm_jmsg
	db	se__,ki__,ti__,ku__,EOM
  else
	db	usf_f,usf_u,usf_c,usf_h,usf_s,usf_i,usf_a,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

guren$:
  ifn	pm_jmsg
	db	gu__,re__,n__,EOM
  else
	db	usf_c,usf_i,usf_n,usf_n,usf_a,usf_b,usf_a,usf_r,spc@,usf_i,usf_s,usf_l,usf_a,usf_n,usf_d,EOM
  endif

sekiei$:
  ifn	pm_jmsg
	db	se__,ki__,e__,i__,EOM
  else
	db	usf_i,usf_n,usf_d,usf_i,usf_g,usf_o,spc@,usf_p,usf_l,usf_a,usf_t,usf_e,usf_a,usf_u,EOM
  endif

yamabuki$:
  ifn	pm_jmsg
	db	ya__,ma__,bu__,ki__,EOM
  else
	db	usf_s,usf_a,usf_f,usf_f,usf_r,usf_o,usf_n,spc@,usf_c,usf_i,usf_t,usf_y,EOM
  endif

road1$:
  ifn	pm_jmsg
	db	n1@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,EOM
  endif

road2$:
  ifn	pm_jmsg
	db	n2@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,EOM
  endif

road3$:
  ifn	pm_jmsg
	db	n3@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n3@,EOM
  endif

road4$:
  ifn	pm_jmsg
	db	n4@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n4@,EOM
  endif

road5$:
  ifn	pm_jmsg
	db	n5@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n5@,EOM
  endif

road6$:
  ifn	pm_jmsg
	db	n6@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n6@,EOM
  endif

road7$:
  ifn	pm_jmsg
	db	n7@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n7@,EOM
  endif

road8$:
  ifn	pm_jmsg
	db	n8@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n8@,EOM
  endif

road9$:
  ifn	pm_jmsg
	db	n9@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n9@,EOM
  endif

road10$:
  ifn	pm_jmsg
	db	n1@,n0@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n0@,EOM
  endif

road11$:
  ifn	pm_jmsg
	db	n1@,n1@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n1@,EOM
  endif

road12$:
  ifn	pm_jmsg
	db	n1@,n2@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n2@,EOM
  endif

road13$:
  ifn	pm_jmsg
	db	n1@,n3@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n3@,EOM
  endif

road14$:
  ifn	pm_jmsg
	db	n1@,n4@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n4@,EOM
  endif

road15$:
  ifn	pm_jmsg
	db	n1@,n5@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n5@,EOM
  endif

road16$:
  ifn	pm_jmsg
	db	n1@,n6@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n6@,EOM
  endif

road17$:
  ifn	pm_jmsg
	db	n1@,n7@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n7@,EOM
  endif

road18$:
  ifn	pm_jmsg
	db	n1@,n8@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n8@,EOM
  endif

road19$:
  ifn	pm_jmsg
	db	n1@,n9@,ba_,n_,su_,i_,do_,u_,EOM
  else
	db	usf_s,usf_e,usf_a,spc@,usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n1@,n9@,EOM
  endif

road20$:
  ifn	pm_jmsg
	db	n2@,n0@,ba_,n_,su_,i_,do_,u_,EOM
  else
	db	usf_s,usf_e,usf_a,spc@,usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n0@,EOM
  endif

road21$:
  ifn	pm_jmsg
	db	n2@,n1@,ba_,n_,su_,i_,do_,u_,EOM
  else
	db	usf_s,usf_e,usf_a,spc@,usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n1@,EOM
  endif

road22$:
  ifn	pm_jmsg
	db	n2@,n2@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n2@,EOM
  endif

road23$:
  ifn	pm_jmsg
	db	n2@,n3@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n3@,EOM
  endif

road24$:
  ifn	pm_jmsg
	db	n2@,n4@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n4@,EOM
  endif

road25$:
  ifn	pm_jmsg
	db	n2@,n5@,ba_,n_,do_,u_,ro_,EOM
  else
	db	usf_r,usf_o,usf_u,usf_t,usf_e,spc@,n2@,n5@,EOM
  endif

mori$:
  ifn	pm_jmsg
	db	to__,ki__,wa__,no_,mo_,ri_,EOM
  else
	db	usf_v,usf_i,usf_r,usf_i,usf_d,usf_i,usf_a,usf_n,spc@,usf_f,usf_o,usf_r,usf_e,usf_s,usf_t,EOM
  endif

otsukimi$:
  ifn	pm_jmsg
	db	o__,tu__,ki__,mi__,ya_,ma_,EOM
  else
	db	usf_m,usf_t,kten@,usf_m,usf_o,usf_o,usf_n,EOM
  endif

iwayama$:
  ifn	pm_jmsg
	db	i__,wa__,ya__,ma__,to__,n__,ne__,ru__,EOM
  else
	db	usf_r,usf_o,usf_c,usf_k,spc@,usf_t,usf_u,usf_n,usf_n,usf_e,usf_l,EOM
  endif

masaki$:
  ifn	pm_jmsg
	db	mi_,sa_,ki_,spc@,no_,spc@,ko_,ya_,EOM
  else
	db	usf_s,usf_e,usf_a,spc@,usf_c,usf_o,usf_t,usf_t,usf_a,usf_g,usf_e,EOM
  endif

santoannu$:
  ifn	pm_jmsg
	db	sa__,n__,to__,nakag@,a__,n__,nu__,go_,u_,EOM
  else
	db	usf_s,kten@,usf_s,kten@,usf_a,usf_n,usf_n,usf_e,EOM
  endif

leage$:
  ifn	pm_jmsg
	db	ri__,bou@,gu__,ho_,n_,bu_,EOM
  else
	db	poke@,usf_m,usf_o,usf_n,spc@,usf_l,usf_e,usf_a,usf_g,usf_u,usf_e,EOM
  endif

tikatuuro$:
  ifn	pm_jmsg
	db	ti_,ka_,tu_,u_,ro_,EOM
  else
	db	usf_u,usf_n,usf_d,usf_e,usf_r,usf_g,usf_r,usf_o,usf_u,usf_n,usf_d,spc@,usf_p,usf_a,usf_t,usf_h,EOM
  endif

tower$:
  ifn	pm_jmsg
	db	poke@,ta__,wa__,bou@,EOM
  else
	db	poke@,usf_m,usf_o,usf_n,spc@,usf_t,usf_o,usf_w,usf_e,usf_r,EOM
  endif

freezer$:
  ifn	pm_jmsg
	db	hu_,ta_,go_,zi_,ma_,EOM
  else
	db	usf_s,usf_e,usf_a,usf_f,usf_o,usf_a,usf_m,spc@,usf_i,usf_s,usf_l,usf_a,usf_n,usf_d,usf_s,EOM
  endif

champ$:
  ifn	pm_jmsg
	db	ti__,yya__,n__,pi__,o__,n__,ro__,bou@,do__,EOM
  else
	db	usf_v,usf_i,usf_c,usf_t,usf_o,usf_r,usf_y,spc@,usf_r,usf_o,usf_a,usf_d,EOM
  endif

digda$:
  ifn	pm_jmsg
	db	de__,ii__,gu__,da__,no_,a_,na_,EOM
  else
	db	usf_d,usf_i,usf_g,usf_l,usf_e,usf_t,usf_t,apt_s_,spc@,usf_c,usf_a,usf_v,usf_e,EOM
  endif

rocket$:
  ifn	pm_jmsg
	db	rocket@,a__,zi__,to__,EOM
  else
	db	usf_r,usf_o,usf_c,usf_k,usf_e,usf_t,spc@,usf_h,usf_q,EOM
  endif

sylpu$:
  ifn	pm_jmsg
	db	si__,ru__,hu__,ho_,n_,si_,yya_,bi__,ru__,EOM
  else
	db	usf_s,usf_i,usf_l,usf_p,usf_h,spc@,usf_c,usf_o,kten@,EOM
  endif

yashiki$:
  ifn	pm_jmsg
	db	poke@,ya_,si_,ki_,EOM
  else
	db	pkmn@,spc@,usf_m,usf_a,usf_n,usf_s,usf_i,usf_o,usf_n,EOM
  endif

safari$:
  ifn	pm_jmsg
	db	sa__,hu__,aa__,ri__,zo__,bou@,n__,EOM
  else
	db	usf_s,usf_a,usf_f,usf_a,usf_r,usf_i,spc@,usf_z,usf_o,usf_n,usf_e,EOM
  endif

wasure$:
  ifn	pm_jmsg
	db	na_,na_,si_,no_,do_,u_,ku_,tu_,EOM
  else
	db	usf_c,usf_e,usf_r,usf_u,usf_l,usf_e,usf_a,usf_n,spc@,usf_c,usf_a,usf_v,usf_e,EOM
  endif

muzin$:
  ifn	pm_jmsg
	db	mu_,zi_,n_,ha_,tu_,de_,n_,si_,yyo_,EOM
  else
	db	usf_p,usf_o,usf_w,usf_e,usf_r,spc@,usf_p,usf_l,usf_a,usf_n,usf_t,EOM
  endif


mapmons:
	db	042h,066h,0ffh,0bdh,099h,0ffh,05ah,024h
;	db	042h,042h,066h,066h,0ffh,0ffh,0bdh,0bdh
;	db	099h,099h,0ffh,0ffh,05ah,05ah,024h,024h


;====================================================;
;	w_map_effect    (ef_count2)                  ;
;	                0  -> jiki_on                ;
;	                25 -> jiki_off               ;
;====================================================;
w_map_effect:
	ld	a,(ef_count2)
	inc	a
	cp	25
	jr	z,off$
	cp	50
	jr	nz,exit$
on$:
	ld	hl,dmy_map
	ld	de,oam_buf
	ld	bc,36*4
	call	block_move
	xor	a
	jr	exit$
off$:
	ld	hl,oam_buf
	ld	b,36
	ld	de,4
loop$:
	ld	(hl),0a0h
	add	hl,de
	dec	b
	jr	nz,loop$
	ld	a,25
exit$:
	ld	(ef_count2),a
	jp	wait_vb


allow_effect2:
	xor	a
	ld	(allow_cnt),a
	ld	b,a
	inc	a
	jr	allow_ef_main
allow_effect:
	ld	hl,sgbcol_buf + 2
	ld	a,(allow_cnt)
	ld	c,a
	ld	b,0
	add	hl,bc

	ld	a,(hl)
allow_ef_main:
	ld	c,a
	ld	hl,time_tbl
	add	hl,bc
	ld	a,(sgb_flg)
	xor	1
	add	a,(hl)
	ld	c,a
	add	a,a
	ld	b,a

	ld	a,(ef_count2)
	and	a
	jr	z,on$
	cp	c
	jr	z,off$
exit$:
	inc	a
	cp	b
	jr	nz,z1$
	xor	a
z1$:
	ld	(ef_count2),a
	jp	wait_vb

on$:
	push	bc
	ld	hl,anime_buf
	ld	de,oam_buf
	ld	bc,24*4
	call	block_move
	pop	bc
	
	xor	a
	jr	exit$
off$:
	push	bc
	ld	hl,oam_buf + 2
	ld	bc,4*4
	ld	a,(allow_cnt)
	call	mul_any

	ld	c,40h
	ld	a,(hl)
	cp	4
	jr	z,tama$
	cp	8
	jr	nz,next$
tama$:
	dec	hl
	dec	hl
	ld	c,1
next$:
	ld	b,4
	ld	de,4
loop$:
	ld	a,(hl)
	add	a,c
	ld	(hl),a
	add	hl,de
	dec	b
	jr	nz,loop$
	pop	bc

	ld	a,c
	jr	exit$
	
time_tbl:
	db	5,16,32

;====================================================;
;	monschr_set 	   	                     ;
;	 IN : position_no                            ;
;====================================================;
	public	monschr_set
	public	monschr_move
	public	monschrsetmain
monschr_move:
	ld	hl,chrset_tbl
;	ld	a,28
	ld	a,30
monschrsetmain:
	ld	bc,0
loop$:
	push	af
	push	bc
	push	hl
	add	hl,bc
	ld	a,(hli)
	ld	e,a
	ld	a,(hli)
	ld	d,a
	ld	a,(hli)
	ld	c,a
	ld	a,(hli)
	ld	b,a
	ld	a,(hli)
	ld	h,(hl)
	ld	l,a

	call	chrmove
	pop	hl
	pop	bc
	ld	a,6
	add	a,c
	ld	c,a
	pop	af
	dec	a
	jr	nz,loop$
	ret

monschr_set:
	call	lcdc_stop
	ld	hl,chrset_tbl
;	ld	a,28
	ld	a,30

	ld	bc,0
loop$:
	push	af
	push	bc
	push	hl
	add	hl,bc
	ld	a,(hli)
	ld	e,a
	ld	a,(hli)
	ld	d,a
	push	de
	ld	a,(hli)
	ld	c,a
	swap	c
	ld	b,0
	ld	a,(hli)

	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	pop	hl

	call	chrset
	pop	hl
	pop	bc
	ld	a,6
	add	a,c
	ld	c,a
	pop	af
	dec	a
	jr	nz,loop$
	jp	lcdc_on



TBLDEF		macro	m_chraddress,mac_bank,mac_size,m_setaddress
	dw	m_chraddress
	db	mac_size
	db	mac_bank
	dw	m_setaddress
		endm

chrset_tbl:
  ifn	YELLOW_VERSION

	TBLDEF	OBJ_MONSTER + 0c0h	,CBANK_MONSTER	,4,8000h ; MONSTER
	TBLDEF	komono			,OBJ_CHARA_BANK2,8,8040h ; CAP/KASEKI
	TBLDEF	OBJ_MARUMON + 0c0h	,CBANK_MARUMON	,4,80c0h ; PIPPI
	TBLDEF	OBJ_TORI + 0c0h		,CBANK_TORI	,4,8100h ; TORI
	TBLDEF	OBJ_UMIRYU		,CBANK_UMIRYU	,4,8140h ; RAPLUS
	TBLDEF	mushi  + 0040h		,G_BANK1c	,1,8180h ; MUSI
	TBLDEF	mushi  + 0050h		,G_BANK1c	,1,81a0h ; MUSI
	TBLDEF	mushi  + 0060h		,G_BANK1c	,1,81c0h ; HANA
	TBLDEF	mushi  + 0070h		,G_BANK1c	,1,81e0h ; HANA
	TBLDEF	mushi  + 0080h		,G_BANK1c	,1,8200h ; HEBI
	TBLDEF	mushi  + 0090h		,G_BANK1c	,1,8220h ; HEBI
	TBLDEF	mushi  + 00a0h		,G_BANK1c	,1,8240h ; YOTUASI
	TBLDEF	mushi  + 00b0h		,G_BANK1c	,1,8260h ; YOTUASI
	TBLDEF	OBJ_PIKACYU    		,CBANK_PIKACYU	,4,8280h ; PIKACYU
	TBLDEF	marumaru      		,G_BANK1c	,4,8380h ; for TUUSHIN

	TBLDEF	OBJ_MONSTER		,CBANK_MONSTER	,4,8400h ; MONSTER
	TBLDEF	komono			,OBJ_CHARA_BANK2,8,8440h ; CAP/KASEKI
	TBLDEF	OBJ_MARUMON		,CBANK_MARUMON	,4,84c0h ; PIPPI
	TBLDEF	OBJ_TORI		,CBANK_TORI	,4,8500h ; TORI
	TBLDEF	OBJ_UMIRYU + 0c0h	,CBANK_UMIRYU	,4,8540h ; RAPLUS
	TBLDEF	mushi  	      		,G_BANK1c	,1,8580h ; MUSI
	TBLDEF	mushi  + 0010h		,G_BANK1c	,1,85a0h ; MUSI
	TBLDEF	mushi  + 0020h		,G_BANK1c	,1,85c0h ; HANA
	TBLDEF	mushi  + 0030h		,G_BANK1c	,1,85e0h ; HANA
	TBLDEF	mushi  + 00c0h		,G_BANK1c	,1,8600h ; HEBI
	TBLDEF	mushi  + 00d0h		,G_BANK1c	,1,8620h ; HEBI
	TBLDEF	mushi  + 00e0h		,G_BANK1c	,1,8640h ; YOTUASI
	TBLDEF	mushi  + 00f0h		,G_BANK1c	,1,8660h ; YOTUASI
	TBLDEF	OBJ_PIKACYU + 0c0h 	,CBANK_PIKACYU	,4,8680h ; PIKACYU
	TBLDEF	marumaru + 0040h	,G_BANK1c	,4,8780h ; for TUUSHIN

  else
	dw	objct  + 06c0h,  5*100h + 4,8000h; MONSTER   Data Address
	dw	komono        ,  4*100h + 8,8040h; CAPSULE   Data Address
	dw	objct6 + 03c0h,  5*100h + 4,80c0h; PIPPI     Data Address
	dw	objct  + 0cc0h,  5*100h + 4,8100h; TORI      Data Address
	dw	objct6 + 0900h,  5*100h + 4,8140h; RAPLUS    Data Address
	dw	mushi  + 0040h,1ch*100h + 1,8180h; MUSI      Data Address
	dw	mushi  + 0050h,1ch*100h + 1,81a0h; MUSI      Data Address
	dw	mushi  + 0060h,1ch*100h + 1,81c0h; HANA      Data Address
	dw	mushi  + 0070h,1ch*100h + 1,81e0h; HANA      Data Address
	dw	mushi  + 0080h,1ch*100h + 1,8200h; HEBI      Data Address
	dw	mushi  + 0090h,1ch*100h + 1,8220h; HEBI      Data Address
	dw	mushi  + 00a0h,1ch*100h + 1,8240h; YOTUASI   Data Address
	dw	mushi  + 00b0h,1ch*100h + 1,8260h; YOTUASI   Data Address
	dw	marumaru      ,1ch*100h + 4,8380h; for	TUUSHIN

	dw	objct  + 0600h,  5*100h + 4,8400h; MONSTER   Data Address
	dw	komono        ,  4*100h + 8,8440h; CAPSULE   Data Address
	dw	objct6 + 0300h,  5*100h + 4,84c0h; PIPPI     Data Address
	dw	objct  + 0c00h,  5*100h + 4,8500h; TORI      Data Address
	dw	objct6 + 09c0h,  5*100h + 4,8540h; RAPLUS    Data Address
	dw	mushi  	      ,1ch*100h + 1,8580h; MUSI      Data Address
	dw	mushi  + 0010h,1ch*100h + 1,85a0h; MUSI      Data Address
	dw	mushi  + 0020h,1ch*100h + 1,85c0h; HANA      Data Address
	dw	mushi  + 0030h,1ch*100h + 1,85e0h; HANA      Data Address
	dw	mushi  + 00c0h,1ch*100h + 1,8600h; HEBI      Data Address
	dw	mushi  + 00d0h,1ch*100h + 1,8620h; HEBI      Data Address
	dw	mushi  + 00e0h,1ch*100h + 1,8640h; YOTUASI   Data Address
	dw	mushi  + 00f0h,1ch*100h + 1,8660h; YOTUASI   Data Address
	dw	marumaru + 0040h,1ch*100h + 4,8780h; for	TUUSHIN
  endif

;====================================================;
;	monschr_put 	   	                     ;
;	 IN : position_no                            ;
;====================================================;
	public	monschr_put
	public	monschr_put2
	public	monsput_namein
monschr_put:
	push	hl
	push	de
	push	bc

	ld	a,(work1)
	cp	0ffh
	jr	z,no_put$

	ld	hl,my_cap_tbl + 1
	ld	e,a
	ld	d,0
	add	hl,de

	ld	a,(hl)

	call	monschrno_get
	ld	(usr_buf + FREE_AREA_@),a	; ld	(usr_buf + 20),a
	call	monsset
ret$:
	pop	bc
	pop	de
	pop	hl
	ret
no_put$:
	ld	hl,oam_buf
	ld	de,anime_buf
	ld	bc,24*4
	call	block_move
	pop	bc
	pop	de
	pop	hl
	ret


monschr_put2:
	xor	a
	ld	(work1),a
	ld	a,(usr_buf + USR_BUF_END -2)	; ld	a,(usr_buf + 27)  ;mons_no
	call	monschrno_get
	ld	(usr_buf + FREE_AREA_@),a	; ld	(usr_buf + 20),a
	jr	monsset


monsput_namein:
	ld	a,(sel_item_no)
	call	monschrno_get

	push	af
	ld	hl,8000h
	call	monmove$
	pop	af
;	add	a,6*14
	add	a,6*15
	ld	hl,8040h
	call	monmove$
	xor	a
	ld	(usr_buf + USR_BUF_END -2),a	; ld	(usr_buf + 27),a
	jr	monschr_put2

monmove$:
	push	hl
	add	a,a
	ld	c,a
	ld	b,0

	ld	hl,chrset_tbl
	add	hl,bc
	add	hl,bc
	add	hl,bc

	ld	a,(hli)
	ld	e,a
	ld	a,(hli)
	ld	d,a
	ld	a,(hli)
	ld	c,a
	ld	a,(hli)
	ld	b,a
	pop	hl
	jp	chrmove


monsset:			;Areg & usr_buf + 20   monster_no
				;work1                 mons_pos
	push	af
	ld	c,10h
	ld	h,> oam_buf
	ld	a,(work1)
	swap	a
	ld	l,a
	add	a,10h
	ld	b,a
	pop	af	
	cp	8
	jr	z,z2$
	call	oam_chrset3
	jr	next$
z2$:
	call	oam_chrset2
next$:
	ld	hl,oam_buf
	ld	de,anime_buf
	ld	bc,24*4
	jp	block_move


monschrno_get:			;Areg monster_no
	ld	(in_dat),a

	ld	a,B_GET_ORDER_NO
	call	bank2bank
	ld	a,(in_dat)
	ld	c,a
	dec	a
	srl	a
	ld	hl,monspattern_tbl$
	ld	e,a
	ld	d,0
	add	hl,de

	ld	a,(hl)
	bit	0,c
	jr	nz,z1$
	swap	a
z1$:
	and	0f0h
	srl	a
	srl	a
	ret


monspattern_tbl$:
	db	077h,070h,000h,055h,056h,066h,066h,064h,044h,099h;20
	db	044h,088h,0aah,000h,000h,000h,000h,033h,099h,033h;40
	db	000h,077h,076h,066h,060h,000h,000h,000h,009h,090h;60
	db	000h,000h,000h,000h,077h,075h,050h,000h,099h,090h;80
	db	011h,044h,045h,050h,002h,020h,000h,080h,005h,051h;100
	db	017h,070h,000h,000h,000h,090h,037h,005h,055h,052h;120
	db	020h,060h,000h,069h,058h,050h,099h,099h,002h,022h;140
	db	024h,004h,044h,088h,080h,000h			 ;150

mushi:
	include		..\effdata\musi.dat
marumaru:
	include		..\effdata\marumaru.dat






;=======================================================================;
; Category	:  ポケモン交換イベント メインルーチン
; Author	: Original by tetsu
; Bank-No.	: G_BANK1c
; In		: usr_buf		trade person id
; Out		: Nothing
; Infomation	: Nothing
;=======================================================================;

;----------------------------------------------------------------
;auto_move_val		: leave-mons number
;auto_move_val + 1~ 2	: trade msg address
;auto_move_val + 3	: trade msg number
;auto_move_val + 4~ 9	: leave-mons name
;auto_move_val +10~15	: get-mons name
;auto_move_val +16~21	: get-mons nick name
;auto_move_val +22	: get-mons number
;================================================================;

BIT_RESET_MODE	equ	0
BIT_SET_MODE	equ	1
BIT_CHECK_MODE	equ	2

TRADE_MSG_OFFER		equ	0
TRADE_MSG_REFUSE	equ	1
TRADE_MSG_NO_MUCH	equ	2
TRADE_MSG_FINISH	equ	3
TRADE_MSG_AFTER		equ	4



	public	monster_trade

monster_trade:
	call	push_vram_m


	ld	hl,mons_trade_tbl
	ld	a,(usr_buf)
	ld	bc,TRADE_DATA_LEN
	call	mul_any

	ld	a,(hli)			; trade_mons_no.
	ld	(auto_move_val + TRADE_MONS_No_@@),a
	ld	a,(hli)			; get_mons_no.
	ld	(auto_move_val + GET_MONS_No_@@),a
	ld	a,(hli)			; msg_kind_no.

	push	af
	ld	de,auto_move_val + PET_NAME_@@		;save pet_name

;--------------------------- blue ordinal program ----------
 ifn  pm_jmsg
;	ld	bc,MONS_NAME_LEN
	ld	bc,4			; 注意！ mons_trade_tbl[] の文字長は全て４文字
	call	block_move
	ld	a,EOM
	ld	(de),a
 else
	ld	bc,MONS_NAME_LEN
	call	block_move
 endif
;--------------------------- blue ordinal program ----------

	pop	af
	ld	l,a
	ld	h,0
	ld	de,trade_msg_tbl
	add	hl,hl
	add	hl,de

	ld	a,(hli)
	ld	(auto_move_val + MSG_ADRS_@@),a		;save msg_address_tbl
	ld	a,(hl)
	ld	(auto_move_val + MSG_ADRS_@@ +1),a

	ld	a,(auto_move_val + TRADE_MONS_No_@@)	;leave_mons_name in str_buf
	ld	de,auto_move_val + TRADE_MONS_NAME_@@
	call	monsname_save

	ld	a,(auto_move_val + GET_MONS_No_@@)
	ld	de,auto_move_val + GET_MONS_NAME_@@
	call	monsname_save


	ld	a,TRADE_MSG_AFTER
	ld	(auto_move_val + MSG_COUNT_@@),a	;msg_count init

	ld	b,BIT_CHECK_MODE
	call	ctrl_comment_flag
	ld	a,c
	and	a
	jr	nz,put_trade_msg

	ld	a,TRADE_MSG_OFFER
	ld	(auto_move_val + MSG_COUNT_@@),a	;msg_count init
	call	put_trade_msg

	ld	a,TRADE_MSG_REFUSE
	ld	(auto_move_val + MSG_COUNT_@@),a	;msg_count init

	call	yes_no
	ld	a,(allow_cnt)
	and	a
	jr	nz,put_trade_msg

	call	mons_trade_main
	jr	c,put_trade_msg
	
	ld	hl,get_msg
	call	put_win_msg

put_trade_msg:
	ld	hl,auto_move_val + MSG_COUNT_@@
	ld	a,(hld)
	ld	e,a
	ld	d,0
	ld	a,(hld)
	ld	l,(hl)
	ld	h,a
	add	hl,de
	add	hl,de

	ld	a,(hli)
	ld	h,(hl)
	ld	l,a
	jp	put_win_msg


monsname_save:
	push	de
	ld	(in_dat),a
	call	get_mons_name
	ld	hl,table_data
	pop	de
	ld	bc,MONS_NAME_LEN
	jp	block_move



 ifn  pm_jmsg
;TRADE_DATA_LEN		equ	0007h
TRADE_DATA_LEN		equ	0008h
 else
TRADE_DATA_LEN		equ	14
 endif

;---------------- name macro define ---------------
 ifn  pm_jmsg
DEFNAME		macro	m1,m2,m3,m4,m5
	DEFCHAR	m1
	DEFCHAR m2
	DEFCHAR	m3
	DEFCHAR	m4
	DEFCHAR	m5
		endm
 else
DEFNAME		macro	m1,m2,m3,m4,m5,m6,m7,m8,m9,m10,m11
	DEFCHAR	m1
	DEFCHAR m2
	DEFCHAR	m3
	DEFCHAR	m4
	DEFCHAR	m5
	DEFCHAR	m6
	DEFCHAR	m7
	DEFCHAR	m8
	DEFCHAR	m9
	DEFCHAR	m10
	DEFCHAR	m11
		endm
 endif

DEFCHAR		macro	m_char
	ifb	<m_char>
	db	EOM
	else
	db	m_char
	endif
		endm

INIT_TRADE_ID	macro	m_number
mmm_counter	=	0
	DEF_TRADE_ID	m_number
		endm

DEF_TRADE_ID	macro	m_number
	public	m_number
m_number	equ	mmm_counter
mmm_counter	=	mmm_counter + 1
		endm
;-------------------------------------------------


mons_trade_tbl:
	INIT_TRADE_ID	matsumiya000
	db	  11,  118			; ベロリンガ→ダグトリオ
	db	YOUNGMSGNO
 ifn  pm_jmsg
	DEFNAME	gu_,ri_,o_,EOM			;	”ぐりお”
 else
	DEFNAME usf_g,usf_u,usf_r,usf_i,usf_o,EOM
 endif
	DEF_TRADE_ID	matsumiya001
	db	4, 42				; ピッピ→バリヤード
	db	YOUNGMSGNO
 ifn  pm_jmsg
	DEFNAME	ma__,i__,mu__			;	”マイル”
 else
	DEFNAME usf_m,usf_i,usf_l,usf_e,usf_s,EOM
 endif

	DEF_TRADE_ID	matsumiya002		; 未使用？
	db	125,114				;supia-      x
	db	LADYMSGNO
 ifn  pm_jmsg
	DEFNAME	ti__,ku__,ti__,ku__		;	”チクチク”
 else
	DEFNAME usf_s,usf_t,usf_i,usf_n,usf_g,usf_e,usf_r,EOM
 endif

	DEF_TRADE_ID	matsumiya003
	db	 2, 136				; ガルーラ→ベトベトン
	db	YOUNGMSGNO
 ifn  pm_jmsg
	DEFNAME	be__,ta__,bo_,u_,EOM		;	”ベタぼう”
 else
	DEFNAME usf_s,usf_t,usf_i,usf_c,usf_k,usf_y,EOM
 endif

	DEF_TRADE_ID	matsumiya004
	db	 21, 21				; ミュウ→ミュウ（削除予定）
	db	LADYMSGNO
 ifn  pm_jmsg
	DEFNAME	ma_,tu_,mi_,ya_			;	”まつみや”
 else
	DEFNAME usf_b,usf_a,usf_r,usf_t,EOM
;	DEFNAME usf_m,usf_a,usf_r,usf_t,usf_y,EOM
 endif

	DEF_TRADE_ID	matsumiya005
	db	30, 46				; モンジャラ→パラセクト 
	db	YOUNGMSGNO
 ifn  pm_jmsg
	DEFNAME	tu_,n_,tu_,n_,EOM		;	”つんつん”
 else
	DEFNAME usf_s,usf_p,usf_i,usf_k,usf_e,EOM
 endif

	DEF_TRADE_ID	matsumiya006		; 削除予定のものです。
	db	 151,151
	db	OLDMSGNO
 ifn  pm_jmsg
	DEFNAME	ma_,tu_,mi_,ya_			;	”まつみや”
 else
	DEFNAME usf_m,usf_a,usf_r,usf_t,usf_y,EOM
;	DEFNAME usf_b,usf_a,usf_r,usf_t,EOM
 endif

	DEF_TRADE_ID	matsumiya007
	db	 128, 1				; ゴルダック→サイドン
	db	OLDMSGNO
 ifn  pm_jmsg
	DEFNAME	go_,n_,su_,ke_			;	”ごんすけ”
 else
	DEFNAME usf_b,usf_u,usf_f,usf_f,usf_y,EOM
 endif

	DEF_TRADE_ID	matsumiya008
	db	 33, 120			; ガーディ→ジュゴン
	db	LADYMSGNO
 ifn  pm_jmsg
	DEFNAME	se__,za__,n__,nu__		;	”セザンヌ”
 else
	DEFNAME usf_c,usf_e,usf_z,usf_a,usf_n,usf_n,usf_e,EOM
 endif

	DEF_TRADE_ID	matsumiya009
	db	17, 41				; カラカラ→ゴーリキー
	db	LADYMSGNO
 ifn  pm_jmsg
	DEFNAME	ri__,ttu__,ki__,bou@		;	”リッキー”
 else
	DEFNAME usf_r,usf_i,usf_c,usf_k,usf_y,EOM
 endif


;================================================================;
;   MONS_TRADE_MAIN                                              ;
;        IN  : auto_move_val =  sellect_monster no.		 ;
;            : sel_item_no   =  get_monster no.			 ;
;            : mons_level    =  get_monster level		 ;
;        OUT : carry_flg   0 =  monter_trade OK 		 ;
;            :		   1 =  monter_trade NG 		 ;
;================================================================;
	public	mons_trade_main		; for debug global label

mons_trade_main:
	xor	a
	ld	(ef_size),a
	dec	a
	ld	(oam_flg),a
	call	cap_list
	push	af
	call	set_chr$
	pop	af
	ld	a,TRADE_MSG_REFUSE
	jp	c,no_mons$		; cancel select mode

	ld	a,(auto_move_val + TRADE_MONS_No_@@)
	ld	b,a
	ld	a,(sel_item_no)
	cp	b
	ld	a,TRADE_MSG_NO_MUCH
	jr	nz,no_mons$		; select pokemon isn't much 
	
	ld	a,(sel_item_pos)
	ld	hl,my_cap_data + 33
	ld	bc,CAPDATA_LEN
	call	mul_any

	ld	a,(hl)			; get_mons_level <-- my_mons_level
	ld	(mons_level),a

	ld	b,BIT_SET_MODE
	call	ctrl_comment_flag

	ld	hl,koukan_msg
	call	put_win_msg

	ld	a,(sel_item_pos)
	push	af
	ld	a,(mons_level)
	push	af

	call	set_gauge
	call	set_koukandata
	ld	a,B_KOUKAN_DEMO_OYA
	call	bank2bank

	pop	af
	ld	(mons_level),a
	pop	af
	ld	(sel_item_pos),a

	ld	a,(auto_move_val + GET_MONS_No_@@)
	ld	(sel_item_no),a

	xor	a
	ld	(my_or_gein),a
	ld	(cap_or_mons),a
	call	sub_capsule

	ld	a,80h
	ld	(my_or_gein),a
	call	add_capsule_new

	call	set_koukanmons

;--------------------------- blue ordinal program ----------
	call	blue_special
;--------------------------- blue ordinal program ----------
	call	dvram_cls

	call	set_chr$

	ld	b,G_BANK3
	ld	hl,cellput_to_bg
	call	bank_push_call

	and	a			;C_flg clear mons_trade OK!
	ld	a,TRADE_MSG_FINISH
	jr	ret$


no_mons$:
	scf				;C_flg set   mons_trade NG!
ret$:
	ld	(auto_move_val + MSG_COUNT_@@),a
	ret




set_chr$:
	call	pal_off_put_wait

	call	set_objdata
	extern	map_rewrite2
	call	map_rewrite2

	call	pop_vram_m

	call	put_wait

	call	color_reset
	ld	c,10
	call	wait_vb_s

	ld	b,G_BANK3
	ld	hl,encount
	jp	bank_push_call






	public	set_koukandata
	public	set_koukanmons

set_koukandata:
	ld	hl,usr_buf
	ld	a,(auto_move_val + TRADE_MONS_No_@@)
	ld	(hli),a				; usr_buf[0] 自分のポケモン番号
	ld	a,(auto_move_val + GET_MONS_No_@@)
	ld	(hl),a				; usr_buf[1] 相手のポケモン番号

	ld	hl,my_cap_oya
	ld	bc,MONS_NAME_LEN
	ld	a,(sel_item_pos)
	call	mul_any

	ld	de,usr_buf + MY_OYA_NAME_@	; ld	de,usr_buf + 4	; usr_buf 4~9（５文字版）
	ld	bc,MONS_NAME_LEN
	call	move$

	ld	hl,aite_name			; aiteno name
	ld	de,usr_buf + YOUR_OYA_NAME_@	; ld	de,usr_buf + 12	; usr_buf 12~17（５文字版）
	call	move$

	ld	de,gein_name 			; usr_buf 12~17
	call	move$

	ld	hl,my_cap_data + 12
	ld	bc,CAPDATA_LEN
	ld	a,(sel_item_pos)
	call	mul_any

	ld	de,usr_buf + MY_MONS_IDNo_@	; ld	de,usr_buf + 10（５文字版）
	ld	bc,2
	call	move$

	extern	rnd
	call	rnd
	ld	hl,random			; 相手のポケモン IDNo. (乱数で決定）aite_id_no
	ld	de,usr_buf + YOUR_MONS_IDNo_@	; ld	de,usr_buf + 18
	jp	block_move

move$:
	push	hl
	push	bc
	call	block_move
	pop	bc
	pop	hl
	ret


set_koukanmons:
	ld	hl,my_cap_name
	ld	bc,MONS_NAME_LEN
	call	set_ram$			; return  DE = HL + BC*((my_cap_tbl)-1)	
	ld	hl,auto_move_val + PET_NAME_@@
	ld	bc, MONS_NAME_LEN		;block_move(&my_cap_name[(my_cap_tbl)-1],
	call	block_move			;	&auto_move_val[PET_NAME_@@], MONS_NAME_LEN)

	ld	hl,my_cap_oya
	ld	bc,MONS_NAME_LEN
	call	set_ram$
	ld	hl,aite_name
	ld	bc, MONS_NAME_LEN		;block_move(&my_cap_oya[(my_cap_tbl)-1],
	call	block_move			;		&aite_name[0], MONS_NAME_LEN)

	ld	hl,my_cap_data + 12		; my_cap_data[][12] == ポケモン IDNo.
	ld	bc,CAPDATA_LEN
	call	set_ram$
	ld	hl,usr_buf + YOUR_MONS_IDNo_@	; ld	hl,usr_buf + 18
	ld	bc,2				;block_move(&my_cap_data[(my_cap_tbl)-1][12],
	jp	block_move			;		&usr_buf[YOUR_MONS_IDNo_@], 2)


set_ram$:
	ld	a,(my_cap_tbl)
	dec	a
	call	mul_any
	ld	e,l
	ld	d,h			; return  DE = HL + BC*((my_cap_tbl)-1)
	ret
	


;-----------------------------------------------
; treat comment_flag bit
; In	: b regs	= 0:BIT_RESET_MODE
;	:  		= 1:BIT_SET_MODE
;	: 		= 2:BIT_CHECK_MODE
;	: usr_buf	trade person number
;-----------------------------------------------
ctrl_comment_flag:
	ld	hl,comment_flag
	ld	a,(usr_buf)
	ld	c,a
	ld	a,B_BIT_CONTROL
	jp	bank2bank



blue_special:
  ifn	YELLOW_VERSION
	ld	a,(auto_move_val + GET_MONS_No_@@)
	cp	038			; goliky old number
	jr	z,evolution$
	cp	039			; gorone old number
	jr	z,evolution$
	cp	041			; yungelar old number
	jr	z,evolution$
	cp	147			; ghost old number
	jr	z,evolution$
	ret
  else
   ifn	pm_jmsg
	ld	a,(auto_move_val + GET_MONS_NAME_@@)
	cp	go__			; groon or ghost
	ret	nz
   else
	;	PARASECT; ０４７ パラセクト		; mons_trade_tbl: を参照
	;	MR.MIME; １２２ バリヤード
	;	BEEDRILL; ０１５ スピアー
	;	KRABBY; ０９８ クラブ
	;	FARFETCH'D; ０８３ カモネギ
	;	TAUROS; １２８ ケンタロス
	;	SPECTRE; ０９３ ゴースト
	;	GRAVELER; ０７５ ゴローン
	;	SLOWMO; ０７９ ヤドン
	;	POLIWAG; ０６０ ニョロモ

	ld	a,(auto_move_val + GET_MONS_NAME_@@)
	cp	usf_g			; GRAVELER; ０７５ ゴローン
	jr	z, _10$
	cp	usf_s			; ゴースト or ヤドン
	ret	nz
	ld	a,(auto_move_val + GET_MONS_NAME_@@ +1)
	cp	usf_p			; SPECTRE; ０９３ ゴースト
	ret	nz
_10$:
   endif
  endif


evolution$:
	ld	a,(my_cap_tbl)
	dec	a
	ld	(sel_item_pos),a

	ld	a,1
	ld	(ctrl_move_val + CANCEL_OK_FLG),a	; cancel ng

	ld	a,50
	ld	(tuushin_flg),a

	ld	hl,shinka_0
	ld	b,G_BANKe
	call	bank_push_call

	xor	a
	ld	(tuushin_flg),a
	
	jp	set_now_music





;------------------------------------------------------
;------------------------------------------------------
aite_name:
	db	trainer@,EOM
 ifn	ASSEMBLE__ENGLISH
	db	EOM,EOM,EOM,EOM,EOM,EOM,EOM,EOM,EOM	; MONS_NAME_LEN (== 11) 転送されるので
 endif							; trade_msg_tbl:以降のゴミデータ
							; 混入を防ぐ為、残りを EOM にしておく
; 注意！）$FE データが混入すると通信交換時の転送で引っかかり "play2.dmg" block_move_loop2$: を参照
; 交換ウィンドウの相手ニックネームの表示等がおかしくなる。


;------------------------------------------------------
; Trade Message Table for youngman/oldman/lady
;------------------------------------------------------
trade_msg_tbl:
	initndw	msg_youngman$	,YOUNGMSGNO
	ndw	msg_oldman$	,OLDMSGNO
	ndw	msg_lady$	,LADYMSGNO

msg_youngman$:
	dw	msg1_0
	dw	msg1_1
	dw	msg1_2
	dw	msg1_3
	dw	msg1_after
;	dw	msg1_4

msg_oldman$:
	dw	msg2_0
	dw	msg2_1
	dw	msg2_2
	dw	msg2_3
	dw	msg2_after
;	dw	msg2_4

msg_lady$:
	dw	msg3_0
	dw	msg3_1
	dw	msg3_2
	dw	msg3_3
	dw	msg3_after
;	dw	msg3_4


;------------------------------------------------------
; Trade Start Message Data
;------------------------------------------------------
koukan_msg:
	extern	koukan_msg_0_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw koukan_msg_0_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


;------------------------------------------------------
; Trade Success Message Data
;------------------------------------------------------
get_msg:
	extern	get_msg_1_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw get_msg_1_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db	FANFARE6
	db	E_WAIT
	db	EOM


;------------------------------------------------------
; Young Man Message Data
;------------------------------------------------------
msg1_0:
	extern	msg1_0_2_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg1_0_2_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg1_1:
	extern	msg1_1_3_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg1_1_3_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg1_2:
	extern	msg1_2_4_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg1_2_4_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg1_3:
	extern	msg1_3_5_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg1_3_5_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg1_after:
	extern	msg1_after_6_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg1_after_6_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


;------------------------------------------------------
; Old Man Message Data
;------------------------------------------------------
msg2_0:
	extern	msg2_0_7_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg2_0_7_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg2_1:
	extern	msg2_1_8_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg2_1_8_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg2_2:
	extern	msg2_2_9_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg2_2_9_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg2_3:
	extern	msg2_3_10_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg2_3_10_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg2_after:
	extern	msg2_after_11_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg2_after_11_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加



;------------------------------------------------------
; Lady Message Data
;------------------------------------------------------
msg3_0:
	extern	msg3_0_12_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg3_0_12_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg3_1:
	extern	msg3_1_13_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg3_1_13_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg3_2:
	extern	msg3_2_14_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg3_2_14_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

msg3_3:
	extern	msg3_3_15_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg3_3_15_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加


msg3_after:
	extern	msg3_after_16_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw msg3_after_16_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加




;========================================================================
;========================================================================

bank3	group	3

	public	distribution_set
	public	badge_put
	public	badge_dat
	public	badge_chrset
	public	anime_xy
;====================================================;
;	distribution set                             ;
;                                                    ;
;====================================================;
distribution_set:
	ld	hl,encount_tbl
	ld	de,yes_no_map
	ld	c,0			;counter
main_loop$:	
	inc	hl
	ld	a,(hld)
;	cp	0ffh			;table_end
	inc	a
	jr	z,end$

	push	hl
	ld	a,(hli)			;area_address set
	ld	h,(hl)
	ld	l,a

	ld	a,(hli)
	and	a
	call	nz,distribution_chk

	ld	a,(hli)
	and	a
	call	nz,distribution_chk

	pop	hl
	inc	hl
	inc	hl
	inc	c
	jr	main_loop$

end$:
	ld	a,0ffh
	ld	(de),a
	ret

distribution_chk:
	inc	hl
	ld	b,10			;table_length/2
loop$:
	ld	a,(in_dat)		;monster_no
	cp	(hl)		
	jr	nz,no_here$

	ld	a,c
	ld	(de),a
	inc	de

no_here$:
	inc	hl
	inc	hl
	dec	b
	jr	nz,loop$

	dec	hl
	ret

	
;====================================================;
;	badge_put  in   my_badge                     ;
;		7-6-5-4-3-2-1-0    bit               ;
;               8-7-6-5-4-3-2-1    badge_no          ;
;====================================================;
badge_chrset:
badge_put:
	ld	de,usr_buf + 2
	ld	hl,leader_adr$		; leader_adr$[] = {20h,28h,30h,38h,40h,48h,50h,58h}
	ld	bc,8
	call	block_move		; usr_buf[2..9] = {20h,28h,30h,38h,40h,48h,50h,58h}

	ld	hl,usr_buf + 12
	ld	bc,8
	xor	a
	call	memset			; usr_buf[12..19] = {0,0,0,0,0,0,0,0}

	ld	de,usr_buf + 12
	ld	hl,usr_buf + 2
	ld	a,(my_badge)		;chrset
	ld	b,a			; B = (my_badge)
	ld	c,8
loop1$:
	srl	b
	jr	nc,next$		; if ( bit<C> ){
	ld	a,(hl)
	add	a,4
	ld	(hl),a			; 	usr_buf[2,,9] += 04h
	ld	a,1
	ld	(de),a			; 	usr_buf[12,,19] = 1
next$:					; }
	inc	hl
	inc	de
	dec	c
	jr	nz,loop1$
	
	ld	hl,usr_buf
 ifn  pm_jmsg
	ld	a,68h + 80h		;number
 else					; 英語版はプレイヤー名に♂♀キャラを使用可にする為に
	ld	a,0D8h			; @〜Gのキャラクタは $D8〜$DF に変更する
 endif					;	参照 "TALKMAP.DMG" status_put()

	ld	(hli),a			;	usr_buf[0] = E8h (== @)
	ld	(hl),60h		;name	usr_buf[1] = 60h

	S_POS	2,11			; １タケシの位置
	ld	de,usr_buf + 12
	call	set$
	S_POS	2,14			; ５キョウの位置
	ld	de,usr_buf + 16
;	call	set$

;	ret


set$:
	ld	c,4
loop$:
	push	de			; &usr_buf[12..]
	push	hl			; S_POS
	ld	a,(usr_buf)		;number_put
	ld	(hli),a			; １回目 S_POS(2,11) = E8h(@) 
	inc	a
	ld	(usr_buf),a		;	usr_buf[0] = E9h(A)

	ld	a,(de)
	and	a
	ld	a,(usr_buf + 1)		;name_put
	jr	nz,no_name$
	call	put$			; １回目 S_POS(3,11)=60h , S_POS(4,11)=61h
	jr	next1$
no_name$:
	inc	a
	inc	a
	inc	hl
next1$:
	ld	(usr_buf + 1),a		; usr_buf[1] += 2
	ld	de,19
	add	hl,de			; １回目 S_POS(3,12) 顔の左上

	ld	a,(usr_buf + 2)		;chr_put
	call	put$			; １回目 S_POS(3,12)=usr_buf[2] , S_POS(3,12)=usr_buf[2]+1
	add	hl,de
	call	put$			; １回目 S_POS(3,13)=usr_buf[2]+2, S_POS(3,13)=usr_buf[2]+3

	push	bc
	ld	hl,usr_buf + 3
	ld	de,usr_buf + 2
	ld	bc,8
	call	block_move		; usr_buf[2..9] = usr_buf[3..10]
	pop	bc
	pop	hl
	ld	de,4
	add	hl,de			; １回目 S_POS(6,11)
	pop	de
	inc	de

	dec	c
	jr	nz,loop$
	ret
	
put$:
	ld	(hli),a
	inc	a
	ld	(hl),a
	inc	a
	ret

leader_adr$:
	db	20h,28h,30h,38h,40h,48h,50h,58h


badge_dat:
;	include	..\effdata\status1.dat
	include	..\data\statusne.dat
	

	public	test_bg_change
	public	bgcell_change
	public	cellput_to_bg
test_bg_change:

;===========================================================;
;     bgcell_change          in   Breg = cell Y_pos         ;
;     	        	          Creg = cell X_pos         ;
;     	        	          el_c = cell no.           ;
;===========================================================;
bgcell_change:
	call	ready2ready
bgcell_c:
	ld	hl,ram_map		;map hidari_ue position keisan
	ld	a,(mapxsize)		;  -> hl
	add	a,6			;   hl = 3*((mapxsize)+6) + 3
	ld	e,a
	ld	d,0
	add	hl,de
	add	hl,de
	add	hl,de
	ld	e,3
	add	hl,de

	ld	e,a			;a = haji_fukumu x_size
	ld	a,b
	and	a
	jr	z,z1$
loop$:
	add	hl,de
	dec	b			;b = y_pos
	jr	nz,loop$
z1$:
	add	hl,bc			;c = x_pos
					;hl <- change_position
	ld	a,(el_c)		;        = cell_address for ram_map
	ld	(hl),a   		;el_c = cell_no

	ld	a,(mapadr2)		;bg_pos for rammap
	ld	c,a
	ld	a,(mapadr2 + 1)
	ld	b,a
	call	cmp_address
	ret	c
	
	push	hl
	ld	l,e
	ld	h,0
	ld	e,6
	ld	d,h
	add	hl,hl
	add	hl,hl
	add	hl,de
	add	hl,bc
	pop	bc
	call	cmp_address
	ret	c

cellput_to_bg:
	ld	a,(fighting_flg)
	inc	a
	ret	z

	ld	a,(all_put_req)
	push	af
	ld	a,(wave_flg)
	push	af
	xor	a
	ld	(all_put_req),a
	ld	(wave_flg),a

	call	put_map
	call	color_rewrite

	ld	hl,mapvramadr
	ld	a,(hli)
	ld	h,(hl)
	ld	l,a

	ld	de,-64
	add	hl,de
	ld	a,h
	and	003h
	or	098h

	ld	a,l
	ld	(yes_no_map),a
	ld	a,h
	ld	(yes_no_map + 1),a

	ld	a,2
	ld	(cell_put_cnt),a
	ld	c,9
loop$:
	push	bc
	push	hl
	push	hl

	ld	hl,dmy_vram -40
	ld	de,20
	ld	a,(cell_put_cnt)
loop1$:
	add	hl,de
	dec	a
	jr	nz,loop1$

	call	puthaji_yoko

	pop	hl
	ld	de,32
	ld	a,(cell_put_cnt)
	ld	c,a
loop2$:
	add	hl,de
	ld	a,h
	and	003h
	or	098h
	dec	c
	jr	nz,loop2$

	ld	(haji_put_buf + 2),a
	ld 	a,l
	ld	(haji_put_buf + 1),a
	ld 	a,2
	ld	(haji_put_buf),a	;yokohaji_put_req
	
	call	wait_vb

	ld	hl,cell_put_cnt
	inc	(hl)
	inc	(hl)

	pop	hl
	pop	bc
	dec	c
	jr	nz,loop$

	pop	af
	ld	(wave_flg),a
	pop	af
	ld	(all_put_req),a
	ret


cmp_address:	; HL - BC
	ld	a,h
	sub	b
	ret	nz
	
	ld	a,l
	sub	c
	ret



;===========================================================;
;     special_command				            ;
;     	        	  iaigiri = bgcell_change           ;
;===========================================================;
	public	spc_iai
	public	efchr_buf_set
	public	iai_check
	extern	musgo
spc_iai:
iai_check:
	xor	a
	ld	(work_event),a

	ld	a,(map_type)		;map_type check
	and	a			;FIELD
	jr	z,next1$		;z = next, nz = return
	cp	7			;GYM
	jr	nz,com_cancel$		;z = next, nz = return
	
	ld	a,(flont_chr)		;flont_chr check
	cp	50h			;chr_no.  (in GYM)
	jr	nz,com_cancel$		;z = bg_change, nz = return
	jr	use$

next1$:
	dec	a
	ld	a,(flont_chr)		;flont_chr check
	cp	3dh			;chr_no.  (in FIELD)
	jr	z,use$			;z = bg_change, nz = return
	cp	52h			;chr_no.
	jr	z,use$			;z = bg_change, nz = return

com_cancel$:
	ld	hl,noiai_msg$
	jp	put_win_msg
	
noiai_msg$:
	extern	noiai_msg_17_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw noiai_msg_17_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加
	

use$:
	ld	(usr_buf + 16),a
	ld	a,1
	ld	(work_event),a

	ld	a,(sel_item_pos)
	ld	hl,my_cap_name
	call	get_pet_name

	ld	hl,obs_system
	set	6,(hl)

	call	pal_off_put_wait
	call	oam_clr

	call	set_objdata
	ld	a,144
	ld	(window_y),a
;	call	put_map
;	call	color_rewrite
	call	put_wait
	call	color_reset

	call	put_map
	call	push_vram_m
;	call	set_objdata
	call	put_wait

	xor	a
	ld	(window_y),a
	ld	hl,iai_msg
	call	put_win_msg

	call	pop_vram_m

	ld	hl,obs_system
	res	6,(hl)

	ld	a,0ffh
	ld	(oam_flg),a

	call	iai_ef

	ld	de,iai_table
	call	flontbg_change
	call	cellput_to_bg

	ld	b,1eh
	ld	hl,iai_effect
	call	bank_push_call

	ld	a,1
	ld	(oam_flg),a

	ld	a,musgo
	call	SEplay

	ld	a,144
	ld	(window_y),a
	call	actor_blanch
	jp	cellput_to_bg


iai_msg:
	extern	iai_msg_18_WORLDMAP
	db I_MSG2	; mvmsg追加
	dw iai_msg_18_WORLDMAP	; mvmsg追加
	db 02dh	; mvmsg追加
	db EOM	; put_msg_s終了コード mvmsg追加

iai_ef:
	xor	a
	ld	(usr_buf + 19),a
	ld	a,0e4h
	ld	(OBP1),a
  ifn	pm_cgb
	call	change_cgb_obp1
  endif
	ld	a,(usr_buf + 16)
	cp	52h
	jr	z,kusa$

	ld	de,townimg + 2d0h
	ld	hl,8fc0h
	ld	bc,19h*100h + 2			; bank number + counter
	call	chrmove				; vram set

	ld	de,townimg + 3d0h
	ld	hl,8fe0h
	ld	bc,19h*100h + 2			; bank number + counter
	call	chrmove				; vram set

	jr	efchr_buf_set

kusa$:
	ld	hl,8fc0h
	call	kusa_set$
	ld	hl,8fd0h
	call	kusa_set$
	ld	hl,8fe0h
	call	kusa_set$
	ld	hl,8ff0h
	call	kusa_set$

	call	efchr_buf_set
	ld	hl,36*4 + 3 + oam_buf
	ld	de,4
	ld	a,30h
	ld	c,e
loop$:
	ld	(hl),a
	add	hl,de
	xor	60h
	dec	c
	jr	nz,loop$
	ret

kusa_set$:
	ld	de,ef1 + 60h
	ld	bc,1eh*100h + 1			; bank number + counter
	jp	chrmove				; vram set

efchr_buf_set:
	call	chrpos_check
	ld	a,9
	ld	de,efchrset_tbl$
	jp	set_oam_buf16

efchrset_tbl$:
  ifn	pm_cgb & pm_cgb_obj_atr
	db	0fch,14h,0fdh,14h,0feh,14h,0ffh,14h
  else
	db	0fch,10h,0fdh,10h,0feh,10h,0ffh,10h
  endif

	public	chrpos_check
chrpos_check:
	ld	hl,actor_table + 4
	ld	a,(hli)		;jiki  Y_pos
	ld	b,a	
	inc	hl
	ld	a,(hli)		;jiki  X_pos
	ld	c,a	
	inc	hl
	inc	hl
	ld	a,(hl)	;jiki muki
	srl	a
	ld	e,a
	ld	d,0

	ld	a,(usr_buf + 19)
	and	a
	ld	hl,iaipos_tbl$
	jr	z,next$
	ld	hl,kairikipos_tbl$
next$:
	add	hl,de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)

	ld	a,b
	add	a,d
	ld	b,a
	ld	a,c
	add	a,e
	ld	c,a
	ret

iaipos_tbl$:
	db	08h,24h,08h,4h,0f8h,14h,18h,14h
kairikipos_tbl$:
	db	08h,34h,08h,0f4h,0e8h,14h,28h,14h


flontbg_change:
	push	de
	ld	a,(mapxsize)		;rammap x_len
	add	a,6			;out_block
	ld	c,a
	ld	b,0
	ld	d,0

	ld	hl,mapadr2
	ld	a,(hli)
	ld	h,(hl)
	ld	l,a

;	ld	a,(mapadr2)		;bg_pos for rammap
;	ld	l,a
;	ld	a,(mapadr2 + 1)
;	ld	h,a

	add	hl,bc

	ld	a,(actor_table + 9)	;jiki muki
	and	a
	jr	z,down$
	cp	4
	jr	z,up$
	cp	8
	jr	z,left$
;	cp	12
;	jr	z,right$

right$:
	ld	a,(mapscloll_x4)
	and	a
	jr	z,here$
	jr	east$
	
down$:
	ld	a,(mapscloll_y4)	;jiki_pos in 1cell
	and	a			;  +-----+-----+ x = mapscloll_x4
	jr	z,here$			;  | x,y | x,y | y = mapscloll_y4
	jr	south$			;  | 0,0 | 1,0 |
up$:					;  +-----+-----+
	ld	a,(mapscloll_y4)	;  | x,y | x,y |
	and	a			;  | 0,1 | 1,1 |
	jr	z,north$		;  +-----+-----+
	jr	here$
left$:
	ld	a,(mapscloll_x4)
	and	a
	jr	z,west$
	jr	here$

south$:
	add	hl,bc			; mapadr2(1block = 1cell)
here$:					; +---+---+---+---+---+---+
	add	hl,bc			; |   |   |   |   |   |   |
north$:					; +---+---+---+---+---+---+
	ld	e,2			; |   |   | N |   |   |   |
	add	hl,de			; +---+---+---+---+---+---+
	jr	kakikae$		; |   | W | H | E |   |   |
west$:					; +---+---+---+---+---+---+
	ld	e,1			; |   |   | S |   |   |   |
	add	hl,bc			; +---+---+---+---+---+---+
	add	hl,de			; |   |   |   |   |   |   |		
	jr	kakikae$		; +---+---+---+---+---+---+
east$:
	ld	e,3
	add	hl,bc
	add	hl,de
;	jr	kakikae$
	
kakikae$:
	pop	de
	ld	a,(hl)
	ld	c,a
loop$:
	ld	a,(de)
	inc	de
	inc	de
	cp	0ffh
	ret	z
	cp	c
	jr	nz,loop$

	dec	de
	ld	a,(de)
	ld	(hl),a
	ret	

iai_table:
	db	50,109		;FIELD
	db	51,108
	db	52,111
	db	53,76
	db	96,110
	db	11,10

	db	3ch,35h		;GYM
	db	3fh,35h
	db	3dh,36h

	db	0ffh


;===========================================================;
;     AUTO_ANIMETION				            ;
;            IN  start pos X,Y = calc_work0,calc_work1      ;
;	         end   pos X,Y = calc_work2,calc_work3      ;
;	     OUT work_anime_buf(in anime_com)  ds 40        ;
;	         anime_cnt     = anmetion count             ;
;===========================================================;
anime_xy:


bank17	group	G_BANK17		;bank1e->bank17へ移動（村川）
;================================================================;
;   SHINKA_ANIMETION                                             ;
;        IN  : yes_no_map + 0   =  old_monster no. 		 ;
;            : yes_no_map + 1   =  new_monster no.		 ;
;================================================================;
	public	shinka_animetion
shinka_animetion:
	push	hl
	push	de
	push	bc
	ld	a,(sel_item_no)
	push	af
	ld	a,(tbl_pos)
	push	af

	xor	a
	ld	(pinchi_flg),a				; Clear pinchi_flag
	ld	(condetion+4),a

	call	MusicStop

	ld	a,1
	ld	(all_put_req),a
	
	ld	a,muswngo
	call	SEplay

	call	put_wait
	xor	a
	ld	(all_put_req),a
	ld	(wave_flg),a
	
	ld	a,(yes_no_map)
	ld	(sgbcol_buf),a
;	ld	b,COL_SHINKA
	ld	c,0
;	call	color_set
	call	shinka_color

	ld	a,(yes_no_map + 1)
	ld	(sel_item_no),a
	ld	(tbl_pos),a

;	call	get_monsadr
;	S_POS	7,2
;	call	prt_mons_chr
	call	shinka_mons_put

	ld	de,9000h
	ld	hl,9310h
	ld	bc,31h
	call	chrmove

	ld	a,(yes_no_map + 0)
	ld	(sel_item_no),a
	ld	(tbl_pos),a

;	call	get_monsadr
;	S_POS	7,2
;	call	prt_mons_chr
	call	shinka_mons_put

	ld	a,1
	ld	(all_put_req),a

	ld	a,(yes_no_map)
	call	gyaarth_play

	call	se_wait

	ld	c,MUSIC_BANK1_NO
	ld	a,musshinka
	call	direct_play

	ld	c,80
	call	wait_vb_s

;	ld	b,COL_SHINKA
	ld	c,1
;	call	color_set
	call	shinka_color

	ld	bc,1*100h + 16
loop$:
	push	bc
	call	wait_s
	jr	c,cancel$

	call	shinka_loop
	pop	bc

	inc	b
	dec	c
	dec	c
	jr	nz,loop$

	xor	a
	ld	(yes_no_map + 3),a
	
	ld	a,49
	ld	(yes_no_map + 2),a
	call	shinka_change

	ld	a,(yes_no_map + 1)
return$:
	ld	(sgbcol_buf),a

	call	MusicStop

	ld	a,(sgbcol_buf)
	call	gyaarth_play

;	ld	b,COL_SHINKA
	ld	c,0
;	call	color_set
	call	shinka_color

	pop	af
	ld	(tbl_pos),a
	pop	af
	ld	(sel_item_no),a
	pop	bc
	pop	de
	pop	hl
	ld	a,(yes_no_map + 3)
	and	a
	ret	z
;	jr	z,ret$
	scf
ret$:
	ret

cancel$:
	pop	bc
	ld	a,1
	ld	(yes_no_map + 3),a
	ld	a,(yes_no_map)
	jr	return$


shinka_color:
	ld	b,COL_SHINKA
	jp	color_set


shinka_mons_put:
	call	get_monsadr
	S_POS	7,2
	jp	prt_mons_chr


shinka_loop:
loop$:
	ld	a,49
	ld	(yes_no_map + 2),a
	call	shinka_change

	ld	a,-49
	ld	(yes_no_map + 2),a
	call	shinka_change

	dec	b
	jr	nz,shinka_loop
	ret	


shinka_change:
	push	bc
	xor	a
	ld	(all_put_req),a
	ld	hl,2*20 + 7 + dmy_vram
	ld	bc,0707h
	ld	de,13
loop$:
	push	bc
loop1$:
	ld	a,(yes_no_map + 2)
	add	a,(hl)
	ld	(hli),a
	dec	c
	jr	nz,loop1$
	pop	bc
	add	hl,de
	dec	b
	jr	nz,loop$

	ld	a,1
	ld	(all_put_req),a
	call	put_wait
	pop	bc	
	ret
	
wait_s:
	call	wait_vb
	
	push	bc
	call	cont_repeat
	ld	a,(joy_repeat)
	pop	bc

	and	02h
	jr	nz,exit$

cannot_cancel$:
	dec	c
	jr	nz,wait_s
	and	a		; c_flg clear
	ret

exit$:
	ld	a,(ctrl_move_val + CANCEL_OK_FLG)
	and	a
	jr	nz,cannot_cancel$
	scf			; c_flg set
	ret


bank15	group	21
;================================================================;
;   HYOUSYOUZYOU  			                         ;
;================================================================;
	public	hyousyoujyou
	extern	Z_CompleteMapMake1
hyousyoujyou:
	call	push_vram_m
	call	pal_off_put_wait
	call	dvram_cls
	xor	a
	ld	(oam_flg),a

	ld	hl,obs_system
	set	6,(hl)

	ld	hl,Z_CompleteMapMake1
	ld	b,G_BANK3a
	call	bank_push_call

	call	cont_abwait

	ld	hl,obs_system
	res	6,(hl)
	call	pal_off_put_wait
	call	map_rewrite2
	call	set_objdata
	call	put_wait
	jp	palset

	

bank10	group	G_BANK10
;================================================================;
;   BIKKURI_MARK  (dealer_encount)                               ;
;        IN  : usr_buf + 18     = actor_table_possition	       	 ;
;            : usr_buf + 19   0 = bikkuri_mark   		 ;
;            :                1 = question_mark			 ;
;            :                2 = nikoniko_mark			 ;
;================================================================;
	public	bikkuri

BIKKURI_MARK	equ	0
QUESTION_MARK	equ	1
NIKONIKO_MARK	equ	2
DOKURO_MARK	equ	3
HEART_MARK	equ	4
THUNDER_MARK	equ	5
ZZZ_MARK	equ	6
SAKANA_MARK	equ	7

bikkuri:
	ld	a,(usr_buf + 19)
	and	00001111b
	swap	a
	ld	c,a
	ld	b,0
	ld	hl,BikkuriChr
	add	hl,bc
	add	hl,bc
	add	hl,bc
	add	hl,bc
	ld	e,l
	ld	d,h
	ld	hl,8f80h			; vram address
	ld	bc,10h*100h + 4			; bank number + counter
	call	chrmove				; vram set

	ld	a,(oam_flg)
	push	af
	ld	a,0ffh
	ld	(oam_flg),a

	ld	a,(obs_map_step)
	bit	6,a

	ld	hl,36*4 - 1 + oam_buf 
	ld	de,40*4 - 1 + oam_buf
	jr	z,set$
	ld	hl,32*4 - 1 + oam_buf
	ld	de,36*4 - 1 + oam_buf

set$:
	ld	bc,36*4

loop$:
	ld	a,(hl)
	ld	(de),a
	dec	hl
	dec	de
	dec	bc
	ld	a,c
	or	b
	jr	nz,loop$
	
	ld	hl,actor_table + 4
	ld	a,(usr_buf + 18)	;table_pos
	swap	a
	ld	c,a
	ld	b,0
	add	hl,bc

	ld	a,(hli)			; actor  Y_pos
	ld	b,a			; bikkuri Y_pos
	inc	hl
	ld	a,(hl)			; actor  X_pos
	add	a,8
	ld	c,a			; bikkuri X_pos

	ld	de,chr_tbl$
	xor	a
	call	set_oam_buf16

;	ld	a,0f7h
;	ld	(usr_buf + 17),a
;	call	set_chr

	ld	c,60
	call	wait_vb_s
	
	pop	af
	ld	(oam_flg),a
	call	wait_vb
	call	actor_blanch

	ret


chr_tbl$:
	db	0f8h,0,0f9h,0,0fah,0,0fbh,0

BikkuriChr:
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,041h	; 0
	db	0FFh,041h,0FFh,041h,0FFh,041h,0FFh,041h
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,082h	; 1
	db	0FFh,082h,0FFh,082h,0FFh,082h,0FFh,082h
	db	0FFh,040h,0FFh,041h,0FFh,041h,07Fh,020h	; 2
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,002h,0FFh,082h,0FFh,082h,0FEh,004h	; 3
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,04Fh	; 4
	db	0FFh,05Ch,0FFh,05Ch,0FFh,041h,0FFh,041h
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,0F2h	; 5
	db	0FFh,03Ah,0FFh,03Ah,0FFh,0E2h,0FFh,082h
	db	0FFh,040h,0FFh,043h,0FFh,043h,07Fh,020h	; 6
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,002h,0FFh,0C2h,0FFh,0C2h,0FEh,004h	; 7
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,040h	; 8
	db	0FFh,04Ch,0FFh,052h,0FFh,052h,0FFh,040h
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,002h	; 9
	db	0FFh,032h,0FFh,04Ah,0FFh,04Ah,0FFh,002h
	db	0FFh,040h,0FFh,044h,0FFh,043h,07Fh,020h	; a
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,002h,0FFh,022h,0FFh,0C2h,0FEh,004h	; b
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,04Fh	; c
	db	0FFh,059h,0FFh,059h,0FFh,05Bh,0FFh,04Fh
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,0F2h	; d
	db	0FFh,09Ah,0FFh,09Ah,0FFh,0DAh,0FFh,0F2h
	db	0FFh,040h,0FFh,045h,0FFh,047h,07Fh,020h	; e
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,002h,0FFh,0A2h,0FFh,0E2h,0FEh,004h	; f
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,04Ch	;10
	db	0FFh,05Eh,0FFh,05Fh,0FFh,05Fh,0FFh,04Fh
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,072h	;11
	db	0FFh,0DAh,0FFh,0EAh,0FFh,0FAh,0FFh,0F2h
	db	0FFh,047h,0FFh,043h,0FFh,041h,07Fh,020h	;12
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,0E2h,0FFh,0C2h,0FFh,082h,0FEh,004h	;13
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,040h	;14
	db	0FFh,043h,0FFh,047h,0FFh,04Fh,0FFh,040h
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,0C2h	;15
	db	0FFh,082h,0FFh,002h,0FFh,0F2h,0FFh,0E2h
	db	0FFh,041h,0FFh,043h,0FFh,046h,07Fh,020h	;16
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,0C2h,0FFh,082h,0FFh,002h,0FEh,004h	;17
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,05Eh	;18
	db	0FFh,044h,0FFh,048h,0FFh,05Eh,0FFh,040h
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,002h	;19
	db	0FFh,002h,0FFh,0AAh,0FFh,002h,0FFh,0F2h
	db	0FFh,040h,0FFh,04Ah,0FFh,040h,07Fh,020h	;1a
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,022h,0FFh,042h,0FFh,0F2h,0FEh,004h	;1b
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h
	db	01Fh,000h,03Fh,01Fh,07Fh,020h,0FFh,040h	;1c
	db	0FFh,040h,0FFh,047h,0FFh,05Fh,0FFh,07Fh
	db	0F8h,000h,0FCh,0F8h,0FEh,004h,0FFh,002h	;1d
	db	0FFh,002h,0FFh,00Ah,0FFh,0DAh,0FFh,0FAh
	db	0FFh,05Fh,0FFh,047h,0FFh,040h,07Fh,020h	;1e
	db	03Fh,01Fh,01Fh,000h,001h,000h,001h,000h
	db	0FFh,0DAh,0FFh,00Ah,0FFh,002h,0FEh,004h	;1f
	db	0FCh,0F8h,0F8h,0C0h,0C0h,080h,080h,000h




bank1f	group	31

	public	zukan_fanfare

	extern	musfanfare
	extern	musfanfare3
	extern	musfanfare6
	extern	musfanfare42
	extern	musfanfare52
	extern	musfanfare82
	extern	musfanfare33
	extern	musfanfare63
	extern	musboboo

zukan_fanfare:
	ld	a,(DA_BUF + 1)
	ld	c,0
	ld	hl,count_tbl$
loop$:
	cp	(hl)
	jr	c,play$
	
	inc	c
	inc	hl
	jr	loop$

play$:
	push	bc

;	ld	a,0ffh
;	ld	(music_flag),a
;	call	se_play
	call	MusicStop

	pop	bc
	ld	b,0
	ld	hl,fanfare_tbl$
	add	hl,bc
	add	hl,bc
	
	ld	a,(hli)
	ld	c,(hl)
	call	direct_play
	jp	set_now_music

fanfare_tbl$:
	db	musboboo	,MUSIC_BANK3_NO
	db	musfanfare3	,MUSIC_BANK1_NO
	db	musfanfare	,MUSIC_BANK1_NO
	db	musfanfare82	,MUSIC_BANK2_NO
	db	musfanfare42	,MUSIC_BANK2_NO
	db	musfanfare6	,MUSIC_BANK1_NO
	db	musfanfare5	,MUSIC_BANK1_NO

count_tbl$:
	db	10,40,60,90,120,150,0ffh

